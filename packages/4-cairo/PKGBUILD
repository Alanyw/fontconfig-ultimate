# $Id: PKGBUILD 161326 2012-06-09 14:55:04Z foutrelis $
# Maintainer: Jan de Groot <jgc@archlinux.org>, bohoomil (mod)
# Contributor: Brice Carpentier <brice@daknet.org>

# Installation order: freetype2, fontconfig, libxft, cairo
pkgname=cairo
pkgver=1.12.2
pkgrel=2
pkgdesc="Cairo vector graphics library"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'libxrender' 'fontconfig' 'pixman' 'glib2' 'sh')
makedepends=('librsvg' 'poppler-glib' 'libspectre' 'gtk-doc') # 'libdrm')
optdepends=('xcb-util: for XCB backend') # really needed?
provides=('cairo-xcb')
replaces=('cairo-xcb')
options=('!libtool')
source=(http://cairographics.org/releases/$pkgname-$pkgver.tar.xz
        cairo-1.10.0-buggy_gradients.patch
        cairo-1.12.2-reduce-broken-stopped-edge-continuation.patch)
md5sums=('87649eb75789739d517c743e94879e51'
         '9b323790dab003e228c6955633cb888e'
         '75ec73746cfaefcbed0e9b2a9f76bf00')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ${srcdir}/cairo-1.10.0-buggy_gradients.patch
  # https://bugs.freedesktop.org/show_bug.cgi?id=50852
  patch -Np1 -i ${srcdir}/cairo-1.12.2-reduce-broken-stopped-edge-continuation.patch
  autoreconf -vfi
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--enable-tee \
	--disable-xlib-xcb \
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
