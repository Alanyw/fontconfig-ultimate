# $Id: PKGBUILD 150857 2012-02-23 13:01:06Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>, bohoomil (mods)

# Installation order: freetype2, fontconfig, libxft, cairo
pkgname=fontconfig
pkgver=2.8.0
pkgrel=2
pkgdesc="A library for configuring and customizing font access"
arch=(i686 x86_64)
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('expat' 'freetype2')
options=('!libtool')
install=fontconfig.install
source=(http://www.fontconfig.org/release/$pkgname-$pkgver.tar.gz
				fontconfig-2.3.2-docbook.patch
				fontconfig-ubuntu.patch
        29-replace-bitmap-fonts.conf
        30-urw-aliases.patch)
md5sums=('77e15a92006ddc2adbb06f840d591c0e'
         '77285f47cde830450543d29365fd6060'
         'ad1986e68925f10f86268f9628d17b22'
         'f6b67e8cc79197ed6abd4701911e83da'
         '51bc6cb633b50f3c28793361738aac5b')

build() {
  cd "$srcdir/$pkgname-$pkgver"
	patch < "$srcdir/fontconfig-2.3.2-docbook.patch"
	patch -Np1 -i "$srcdir/fontconfig-ubuntu.patch"
	patch -Np1 -i "$srcdir/30-urw-aliases.patch"
  # enable Position Independent Code for prelinking
  export CFLAGS="$CFLAGS -fPIC"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -m644 "$srcdir/29-replace-bitmap-fonts.conf" \
      "$pkgdir/etc/fonts/conf.avail"
  rm -f "$pkgdir"/etc/fonts/conf.d/*.conf

  #Install license
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}

