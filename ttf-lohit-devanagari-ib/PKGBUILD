# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-lohit-devanagari-ib
_name=lohit-devanagari-ttf
pkgver=2.95.2
pkgrel=1
depends=('fontconfig')
pkgdesc="Lohit Devanagari font."
url="https://fedorahosted.org/lohit/"
arch=('any')
conflicts=('lohit-fonts')
groups=('infinality-bundle-fonts')
license=('custom:OFL')
source=("https://fedorahosted.org/releases/l/o/lohit/${_name}-${pkgver}.tar.gz"
        40-lohit-devanagari.conf
        66-lohit-devanagari.conf
        90-tt-lohit-devanagari.conf)
sha1sums=('834a03f6075763745148d0c0e79b41175f44b500'
          'ec03746a621de6456c0a412f392f762a3bea92e1'
          '2682411eefea9016663af2028196c22f89a18513'
          '3bd004a6eefef9eae20dbf2d2cb2d65e816ead30')

package(){
  cd "${_name}-${pkgver}"

  install -D -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -D -m644 40-lohit-devanagari.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-lohit-devanagari.conf
  install -D -m644 66-lohit-devanagari.conf \
    "${pkgdir}"/etc/fonts/conf.avail/66-lohit-devanagari.conf
  install -D -m644 90-tt-lohit-devanagari.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-lohit-devanagari.conf

  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-lohit-devanagari.conf .
  ln -s ../conf.avail/66-lohit-devanagari.conf .
  ln -s ../conf.avail/90-tt-lohit-devanagari.conf .
}
