# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>
# Maintainer: bohoomil <@zoho.com>

pkgname=dejavu-ib
pkgname=('t1-dejavu-ib' 'ttf-dejavu-ib')
pkgver=2.34
pkgrel=9
pkgdesc="Font family based on the Bitstream Vera Fonts with a wider range of characters. Re-packed for infinality-bundle-fonts."
arch=('any')
url="http://dejavu-fonts.org/wiki/Main_Page"
license=('custom')
depends=('fontconfig')
source=(http://mirrors.ctan.org/fonts/dejavu.zip
        http://downloads.sourceforge.net/project/dejavu/dejavu/${pkgver}/dejavu-fonts-ttf-${pkgver}.tar.bz2
        45-dejavu.conf
        90-non-tt-dejavu.conf
        90-tt-dejavu.conf)
sha1sums=('f77232e447079a269dae426ea00460849a68634e'
          '33fa825aa1dd44ce23a11af92b3078e978ff513d'
          '4072385eb6d18754f932bda5634da259ab875a1b'
          'b8fafbd2622046a312d1dffc32f7e017aeb24fae'
          'c6ff96b61d8905a471a108302731edc4352622ce')

package_t1-dejavu-ib() {
  pkgdesc="Font family based on the Bitstream Vera Fonts with a wider range of characters. Re-packed for infinality-bundle-fonts. Type 1 version."
  groups=('infinality-bundle-fonts')
  conflicts=('ttf-dejavu' 'ttf-dejavu-ib')
  provides=('ttf-font' 'ttf-dejavu' 'dejavu-ib')
  replaces=('ttf-dejavu')
  install=${pkgname}.install

  cd "${srcdir}"/dejavu

  install -dm755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 afm/*.afm "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 type1/*.{pfb,pfm} "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -Dm644 doc/font-doc/LICENSE \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -dm755 "${pkgdir}"/etc/fonts/conf.avail
  install -dm755 "${pkgdir}"/etc/fonts/conf.d
  install -m644 "${srcdir}"/45-dejavu.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-dejavu.conf
  install -m644 "${srcdir}"/90-non-tt-dejavu.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-dejavu.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-dejavu.conf .
  ln -s ../conf.avail/90-non-tt-dejavu.conf .
}

package_ttf-dejavu-ib() {
  pkgdesc="Font family based on the Bitstream Vera Fonts with a wider range of characters. Re-packed for infinality-bundle-fonts. TrueType version."
  groups=('infinality-bundle-fonts')
  conflicts=('ttf-dejavu' 't1-dejavu-ib')
  provides=('ttf-font' 'ttf-dejavu' 'dejavu-ib')
  replaces=('ttf-dejavu')
  install=${pkgname}.install

  cd "${srcdir}"/dejavu-fonts-ttf-${pkgver}
  install -dm755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 ttf/*.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -dm755 "${pkgdir}"/etc/fonts/conf.avail
  install -dm755 "${pkgdir}"/etc/fonts/conf.d
  install -m644 "${srcdir}"/45-dejavu.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-dejavu.conf
  install -m644 "${srcdir}/"90-tt-dejavu.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-dejavu.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-dejavu.conf .
  ln -s ../conf.avail/90-tt-dejavu.conf .
}

