# $Id: PKGBUILD 193626 2013-08-26 08:21:28Z jgc $
# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>
# Contributor: bohoomil <@zoho.com>

pkgname=dejavu-ib
pkgname=('t1-dejavu-ib' 'ttf-dejavu-ibx')
pkgver=2.34
pkgrel=4
pkgdesc="Font family based on the Bitstream Vera Fonts with a wider range of characters. Re-packed for infinality-bundle-fonts."
arch=('any')
url="http://dejavu-fonts.org/wiki/Main_Page"
license=('custom')
depends=('fontconfig')
provides=('ttf-font' 'ttf-dejavu')
replaces=('ttf-dejavu')
source=(http://mirrors.ctan.org/fonts/dejavu.zip
        http://downloads.sourceforge.net/project/dejavu/dejavu/${pkgver}/dejavu-fonts-ttf-${pkgver}.tar.bz2
        90-dejavu-t1.conf
        90-dejavu-ttf.conf)

package_t1-dejavu-ib() {
  pkgdesc="Font family based on the Bitstream Vera Fonts with a wider range of characters. Re-packed for infinality-bundle-fonts. Type 1 version."
  groups=('infinality-bundle-fonts')
  conflicts=('ttf-dejavu' 'ttf-dejavu-ibx')
  install=${pkgname}.install

  cd "${srcdir}"/dejavu
 
  install -dm755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 afm/*.afm "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 type1/*.{pfb,pfm} "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -Dm644 doc/font-doc/LICENSE \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -dm755 "${pkgdir}"/etc/fonts/conf.avail
  install -dm755 "${pkgdir}"/etc/fonts/conf.d
  install -m644 "${srcdir}"/90-dejavu-t1.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-dejavu-t1.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/90-dejavu-t1.conf .
}

package_ttf-dejavu-ibx() {
  pkgdesc="Font family based on the Bitstream Vera Fonts with a wider range of characters. Re-packed for infinality-bundle-fonts. TrueType version."
  groups=('infinality-bundle-fonts-extra')
  conflicts=('ttf-dejavu' 't1-dejavu-ib')
  install=${pkgname}.install

  cd "${srcdir}"/dejavu-fonts-ttf-${pkgver}
  install -dm755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 ttf/*.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -dm755 "${pkgdir}"/etc/fonts/conf.avail
  install -dm755 "${pkgdir}"/etc/fonts/conf.d
  install -m644 "${srcdir}/"90-dejavu-ttf.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-dejavu-ttf.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/90-dejavu-ttf.conf .
}

md5sums=('c5a9d02952dce3792e34d29134f1d991'
         '161462de16e2ca79873bc2b0d2e6c74c'
         '0c498a66a13928e01d36362e0206d61b'
         '1cf1231685f5fb61dacad979268c7a53')
