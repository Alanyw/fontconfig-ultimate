# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-playfair-display-ibx
pkgver=1.002
pkgrel=3
depends=('fontconfig')
pkgdesc="An Open Source typeface family for display and titling use."
url="http://www.forthehearts.net/"
arch=('any')
groups=('infinality-bundle-fonts-extra')
license=('custom:OFL')
source=(http://bohoomil.com/src/Playfair_Display.zip{,.sig}
        http://bohoomil.com/src/Playfair_Display_SC.zip{,.sig}
        45-playfair-display.conf
        90-tt-playfair-display.conf)
validgpgkeys=('A9244FB5E93F11F0E975337FAE6866C7962DDE58')
sha1sums=('4d4c950e0e011cab8377d3532a2603d30ef1bb59'
          'SKIP'
          'bd8ef3073012164ac14c416e085e29250c193ff0'
          'SKIP'
          '83dc863c3787b61c2a4b3daf444ad374ffdf1cb7'
          'ff084105cd1d52124698c7c155aa7b4be932c8ed')

package(){

  install -D -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -D -m644 45-playfair-display.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-playfair-display.conf
  install -D -m644 90-tt-playfair-display.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-playfair-display.conf

  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-playfair-display.conf .
  ln -s ../conf.avail/90-tt-playfair-display.conf .
}
