# Maintainer: bohoomil <@zoho.com>

pkgname=t1-inconsolata-zi4-ibx
pkgver=1.03
pkgrel=4
depends=('fontconfig')
pkgdesc="Monospace font for pretty code listings and for the terminal. Type 1 version."
url="http://www.ctan.org/tex-archive/fonts/inconsolata/"
arch=('any')
license=('OFL-1.1')
groups=('infinality-bundle-fonts-extra')
replaces=('t1-inconsolata-zi4')
conflicts=('t1-inconsolata-zi4' 'ttf-inconsolata' 'ttf-inconsolata-g' 'ttf-inconsolata-dz')
install=$pkgname.install
source=("http://mirrors.ctan.org/fonts/inconsolata.zip")

prepare(){
  cd "${srcdir}"/inconsolata/type1
  type1afm *.pfb
}

package(){
  cd "${srcdir}"/inconsolata

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 doc/OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 type1/*.{pfb,afm} "${pkgdir}"/usr/share/fonts/"${pkgname}"
}

md5sums=('b0c2e9400b829f7100259a653b0ba815')
