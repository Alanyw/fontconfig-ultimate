# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>
# Maintainer: bohoomil <@zoho.com>

# Installation order: freetype2-infinality-ultimate fontconfig-infinality-ultimate cairo-infinality-ultimate
pkgname=cairo-infinality-ultimate
_pkgbasename=cairo
pkgver=1.12.16
pkgrel=2
groups=('infinality-bundle')
pkgdesc="Cairo vector graphics library with respect-fontconfig, expose and Ubuntu patches (infinality-bundle)"
arch=('i686' 'x86_64')
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman>=0.28.0' 'glib2' 'mesa' 'libgl' 'sh' 'lzo2')
makedepends=('mesa-libgl' 'librsvg' 'gtk2' 'poppler-glib' 'libspectre' 'gtk-doc' 
             'valgrind'
             # for the test suite:
              'ttf-dejavu' 'gsfonts' 'xorg-server-xvfb' ) # 'libdrm')
provides=("cairo=$pkgver" 'cairo-xcb' 'cairo-ubuntu')
replaces=('cairo-xcb')
conflicts=('cairo' 'cairo-cleartype' 'cairo-git' 'cairo-gl-git' 'cairo-glitz' 
           'cairo-ocaml-git' 'cairo-small' 'cairo-ubuntu')
options=('!libtool')
source=(http://cairographics.org/releases/$_pkgbasename-$pkgver.tar.xz
        cairo-respect-fontconfig.patch
        cairo-1.12.8-expose_snapshot-1.patch
        http://archive.ubuntu.com/ubuntu/pool/main/c/cairo/cairo_1.12.14-0ubuntu1.debian.tar.gz)

prepare(){
  cd "$srcdir/$_pkgbasename-$pkgver"

  patch -Np1 -i $srcdir/cairo-respect-fontconfig.patch
  patch -Np1 -i $srcdir/cairo-1.12.8-expose_snapshot-1.patch

  for _f in $(cat $srcdir/debian/patches/series) ; do    
    patch -Np1 -i $srcdir/debian/patches/$_f    
  done

  sed '20 aAM_PROG_AR' -i $srcdir/$_pkgbasename-$pkgver/configure.ac
}

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  autoreconf -if

  # config options
  # --enable-xlib-xcb \ 

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-tee \
    --enable-gl \
    --enable-egl \
    --enable-svg \
    --enable-ps \
    --enable-pdf \
    --enable-gobject
  make
}

#check() {
#  cd "$srcdir/$pkgname-$pkgver"
#  make -k check || /bin/true # 162 Passed, 328 Failed [8 crashed, 10 expected], 26 Skipped
#}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install
}

md5sums=('a1304edcdc99282f478b995ee5f8f854'
         '485a80383e70d8631e2355c46f3324a4'
         '92bb0af20e9438d07a04a586841ac90f'
         '2465d76908c35aae2be04df33c41064b')
