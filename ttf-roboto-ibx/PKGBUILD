# Mainainer: bohoomil <@zoho.com>

pkgname=ttf-roboto-ibx
pkgver=2.0
pkgrel=8
pkgdesc="Modern sans-serif font family designed by Christian Robertson and introduced with Android Ice Cream Sandwich operating system. Basic, condensed and slab variants included."
arch=('any')
license=('Apache')
url="http://code.google.com/android/"
depends=('fontconfig')
groups=('infinality-bundle-fonts-extra')
conflicts=('ttf-roboto' 'ttf-roboto-fontconfig' 'ttf-roboto-slab')
# http://www.google.com/fonts
# all scripts
source=(http://bohoomil.com/src/Roboto_131072_fixed.zip{,.sig}
        http://bohoomil.com/src/Roboto_Condensed.zip{,.sig}
        http://bohoomil.com/src/Roboto_Slab.zip{,.sig}
        45-roboto.conf
        90-tt-roboto.conf)
validgpgkeys=('A9244FB5E93F11F0E975337FAE6866C7962DDE58')
sha1sums=('5156a2b332f73750c89fb2e89f44dffaf0ab132b'
          'SKIP'
          '6cd4aad2ddb3c0022adc65c96ab85c4557f7b308'
          'SKIP'
          '5a51edafb95613fcf8ab7ab32af155f25cf379ee'
          'SKIP'
          '8ff0fd3af47b4234cdb40746a29aa613fc8dc502'
          '05cffa42f1ac56bf5595afe2ab67e656555273fb')

package(){
  install -D -m644 "LICENSE.txt" \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -d -m755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -D -m644 45-roboto.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-roboto.conf
  install -D -m644 90-tt-roboto.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-roboto.conf

  install -d "${pkgdir}"/etc/fonts/conf.d
  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-roboto.conf .
  ln -s ../conf.avail/90-tt-roboto.conf .
}
