# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-croscore-fonts-ibx
pkgbase=noto-fonts
_pkgver=2015-12-24
_commit=3f1dc1
pkgver=${_pkgver//-}
pkgrel=1
depends=('fontconfig')
pkgdesc="Google Chrome OS core fonts."
url="http://code.google.com/p/chromium-os/issues/detail?id=5287"
arch=('any')
groups=('infinality-bundle-fonts-extra')
conflicts=('otf-google-fonts-hg' 'ttf-google-fonts-git' 'ttf-google-fonts-hg' 'ttf-chromeos-fonts' 'ttf-chromeos-extra-fonts' 'ttf-chromeos-ibx' 'noto-fonts' 'ttf-noto-fonts-ib')
replaces=('ttf-chromeos-ibx')
license=('Apache')
source=(https://github.com/googlei18n/noto-fonts/archive/$_commit.zip
        45-croscore-fonts.conf
        90-tt-croscore-fonts.conf)
sha1sums=('4d61bcc743b07adbe4f115d2283f65f402d00d99'
          '23410520226bc33f24a7b0ce315fd3f42aa13bee'
          '6baec77ed28c068996566f0287652ca165c703ee')

package(){
  cd "${pkgbase}"-*/hinted

  install -D -m644 ../LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 Arimo*.ttf Cousine*.ttf Tinos*.ttf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -D -m644 45-croscore-fonts.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-croscore-fonts.conf
  install -D -m644 90-tt-croscore-fonts.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-croscore-fonts.conf

  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-croscore-fonts.conf .
  ln -s ../conf.avail/90-tt-croscore-fonts.conf .
}
