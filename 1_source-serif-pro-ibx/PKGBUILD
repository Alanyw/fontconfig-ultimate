# Maintainer: bohoomil <@zoho.com>

pkgname=source-serif-pro-ibx
pkgname=('otf-source-serif-pro-ibx' 'ttf-source-serif-pro-ibx')
pkgver=1.014
pkgrel=2
depends=('fontconfig')
pkgdesc="Source Serif Pro is a serif typeface in the transitional style, designed to complement Source Sans."
url="http://www.adobe.com/"
arch=('any')
license=('OFL-1.1')
groups=('infinality-bundle-fonts-extra')
source=("http://downloads.sourceforge.net/project/sourceserifpro.adobe/SourceSerifPro_FontsOnly-${pkgver}.zip"
        45-source-serif-pro.conf
        90-non-tt-source-serif-pro.conf
        90-tt-source-serif-pro.conf)
sha1sums=('293c8dc709b80271becc4a07a7ec87049a6715b6'
          'ec79743b49b83d380ca86918c7c44d474fd5101d'
          '9a18aba32c75d4a9b9cedda6d6115124804b5241'
          '19dfa23495d142c728f8947b5eefd169f495e82e')

package_otf-source-serif-pro-ibx(){
  pkgdesc="Source Serif Pro is a serif typeface in the transitional style, designed to complement Source Sans. OpenType version."
  conflicts=('adobe-source-serif-pro-fonts' 'ttf-source-serif-pro-ibx')
  install=$pkgname.install

  cd "${srcdir}"/source-serif-pro-1.014R

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 OTF/*.otf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-source-serif-pro.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-source-serif-pro.conf
  install -m644 90-non-tt-source-serif-pro.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-source-serif-pro.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-source-serif-pro.conf .
  ln -s ../conf.avail/90-non-tt-source-serif-pro.conf .
}

package_ttf-source-serif-pro-ibx(){
  pkgdesc="Source Serif Pro is a serif typeface in the transitional style, designed to complement Source Sans. TrueType version."
  conflicts=('adobe-source-serif-pro-fonts' 'otf-source-serif-pro-ibx')
  install=$pkgname.install

  cd "${srcdir}"/source-serif-pro-1.014R

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 TTF/*.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-source-serif-pro.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-source-serif-pro.conf
  install -m644 90-tt-source-serif-pro.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-source-serif-pro.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-source-serif-pro.conf .
  ln -s ../conf.avail/90-tt-source-serif-pro.conf .
}
