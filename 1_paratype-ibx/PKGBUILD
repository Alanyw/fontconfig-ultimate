# Maintainer: bohoomil <@zoho.com>

pkgname=paratype-ibx
pkgname=('t1-paratype-ibx' 'ttf-paratype-ibx')
pkgver=2014.06.10
pkgrel=1
depends=('fontconfig')
pkgdesc="Paratype font family."
groups=('infinality-bundle-fonts-extra')
url="http://www.paratype.com/public/"
arch=('any')
license=('Custom')
source=("http://mirrors.ctan.org/fonts/paratype.zip"
        "http://www.fontstock.com/public/PTSans.zip"
        "http://www.fontstock.com/public/PTSerif.zip"
        "http://www.fontstock.com/public/PTMono.zip"
        45-paratype.conf
        90-non-tt-paratype.conf
        90-tt-paratype.conf)
sha1sums=('2a33ef4956bb4b5144a093724b4614a282261ca9'
          '3e14c921c2c9eb524ad5680eff931d1d5cbca00a'
          'b445d4cc49a18581852bab27a204d4b4a44a403f'
          '1e06f32df070d93adb1b5f5b515d8af770b6cd28'
          'fb57f1cebb077edd79bbba1648f5f7ce96b5f741'
          'c2ce354aeab52f9e8c08c1168c80f95b561fefb8'
          '7cc56afa20746f1fada3a309575eacd812a0522e')

package_t1-paratype-ibx(){
  pkgdesc="Paratype font family. Type 1 version (CTAN)."
  conflicts=('ttf-paratype' 'ttf-ptsans' 'ttf-pt-mono' 'ttf-pt-sans' 'ttf-pt-serif' 'ttf-paratype-ibx')
  install=$pkgname.install
  cd "${srcdir}"/paratype

  install -m755 -d "${pkgdir}"/usr/share/doc/"${pkgname}"
  install -m644 doc/CHANGELOG "${pkgdir}"/usr/share/doc/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 doc/PT_Free_Font_License_eng_1.3.txt \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 type1/*.{pfb,pfm} "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 afm/*.{afm,inf} "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../45-paratype.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-paratype.conf
  install -m644 ../90-non-tt-paratype.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-paratype.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-paratype.conf .
  ln -s ../conf.avail/90-non-tt-paratype.conf .
}

package_ttf-paratype-ibx(){
  pkgdesc="Paratype font family. TrueType version."
  conflicts=('ttf-paratype' 'ttf-ptsans' 'ttf-pt-mono' 'ttf-pt-sans' 'ttf-pt-serif' 't1-paratype-ibx')
  install=$pkgname.install

  cd "${srcdir}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "PT Free Font License_eng_1.2.txt" \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "PT Free Font License_eng_1.3.txt" \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-paratype.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-paratype.conf
  install -m644 90-tt-paratype.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-paratype.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-paratype.conf .
  ln -s ../conf.avail/90-tt-paratype.conf .
}
