# Maintainer: bohoomil <@zoho.com>

pkgbase=otf-fira-fonts
pkgver=3.111
pkgrel=2
pkgname=('otf-fira-sans-ibx' 'otf-fira-mono-ibx')
depends=('fontconfig')
pkgdesc="Mozilla font family designed for the Firefox OS."
url="https://www.mozilla.org/en-US/styleguide/products/firefox-os/typeface/"
arch=('any')
license=('Apache')
groups=('infinality-bundle-fonts-extra')
source=("http://www.carrois.com/wordpress/downloads/fira_3_1/FiraFonts3111.zip"
        45-fira-sans.conf
        45-fira-mono.conf
        90-non-tt-fira-sans.conf
        90-non-tt-fira-mono.conf)
sha1sums=('242cad3019d6c57fc4ed1c1e4bbb95407e1de062'
          'aebc44373e661b95d07b6159d4f958321d8a2e87'
          'd8a085a8e70c28f405dc3ed0d60a5d44d045c86c'
          '38bc787c66433f10f368896fb93c70783de4fbcc'
          'e45c23621fc2676bc580b1242b426157d71ad521')

package_otf-fira-sans-ibx() {
  pkgdesc="A sans-serif typeface designed for the Firefox OS."
  conflicts=('ttf-fira-sans' 'otf-fira-sans' 'ttf-fira-sans-ibx')
  replaces=('otf-fira-sans')
  install=${pkgname}.install

  cd "${srcdir}"/FiraFonts3111/FiraSans3111/OTF
  install -d -m755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 FiraSans-*.otf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-fira-sans.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-fira-sans.conf
  install -m644 90-non-tt-fira-sans.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-fira-sans.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/90-non-tt-fira-sans.conf .
  ln -s ../conf.avail/45-fira-sans.conf .
}

package_otf-fira-mono-ibx() {
  pkgdesc="A monospace typeface designed for the Firefox OS."
  conflicts=('ttf-fira-mono' 'otf-fira-mono' 'ttf-fira-mono-ibx')
  replaces=('otf-fira-mono')
  install=${pkgname}.install

  cd "${srcdir}"/FiraFonts3111/FiraMono3111/OTF
  install -d -m755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 FiraMono-*.otf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-fira-mono.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-fira-mono.conf
  install -m644 90-non-tt-fira-mono.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-fira-mono.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/90-non-tt-fira-mono.conf .
  ln -s ../conf.avail/45-fira-mono.conf .
}

