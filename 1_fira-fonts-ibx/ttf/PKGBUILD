# Maintainer: bohoomil <@zoho.com>

pkgbase=ttf-fira-fonts
pkgver=3.111
pkgrel=1
pkgname=('ttf-fira-sans-ibx' 'ttf-fira-mono-ibx')
depends=('fontconfig')
pkgdesc="Mozilla font family designed for the Firefox OS."
url="https://www.mozilla.org/en-US/styleguide/products/firefox-os/typeface/"
arch=('any')
license=('Apache')
groups=('infinality-bundle-fonts-extra')
source=("http://www.carrois.com/wordpress/downloads/fira_3_1/FiraFonts3111.zip"
        45-fira-sans.conf
        45-fira-mono.conf
        90-tt-fira-sans.conf
        90-tt-fira-mono.conf)
sha1sums=('242cad3019d6c57fc4ed1c1e4bbb95407e1de062'
          'aebc44373e661b95d07b6159d4f958321d8a2e87'
          'd8a085a8e70c28f405dc3ed0d60a5d44d045c86c'
          '4c431e944840726afea8683a50a8b70a5abcb38a'
          'b2970ffff80f13bb73ae05e1d9e858aa016aa6ad')

package_ttf-fira-sans-ibx() {
  pkgdesc="A sans-serif typeface designed for the Firefox OS."
  conflicts=('ttf-fira-sans' 'otf-fira-sans' 'otf-fira-sans-ibx')
  replaces=('ttf-fira-sans')
  install=${pkgname}.install

  cd "${srcdir}"/FiraFonts3111/FiraSans3111/WEB
  install -d -m755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 FiraSans-*.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-fira-sans.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-fira-sans.conf
  install -m644 90-tt-fira-sans.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-fira-sans.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/90-tt-fira-sans.conf .
  ln -s ../conf.avail/45-fira-sans.conf .
}

package_ttf-fira-mono-ibx() {
  pkgdesc="A monospace typeface designed for the Firefox OS."
  conflicts=('ttf-fira-mono' 'otf-fira-mono' 'otf-fira-mono-ibx')
  replaces=('ttf-fira-mono')
  install=${pkgname}.install

  cd "${srcdir}"/FiraFonts3111/FiraMono3111/WEB
  install -d -m755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 FiraMono-*.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-fira-mono.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-fira-mono.conf
  install -m644 90-tt-fira-mono.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-fira-mono.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/90-tt-fira-mono.conf .
  ln -s ../conf.avail/45-fira-mono.conf .
}

