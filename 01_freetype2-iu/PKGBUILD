# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: JIN Xiao-Yong <jinxiaoyong@gmail.com>
# Maintainer: bohoomil <@zoho.com>

pkgname=freetype2-infinality-ultimate
pkgver=2.5.5
pkgrel=5
_patchrel=2015.04.26
pkgdesc="TrueType font rendering library with Infinality patches and custom settings."
arch=('i686' 'x86_64')
changelog=CHANGELOG
license=('GPL' 'MIT')
groups=('infinality-bundle')
url="http://freetype.sourceforge.net"
depends=('zlib' 'bzip2' 'sh' 'xorg-xrdb' 'libpng' 'harfbuzz')
conflicts=('freetype2' 'freetype2-infinality')
provides=("freetype2=$pkgver" 'freetype2-infinality' 'freetype2-infinality-ultimate')
backup=('etc/X11/xinit/xinitrc.d/infinality-settings.sh')
install='infinality.install'
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        infinality-settings.sh
        "01-freetype-${pkgver}-enable-valid.patch"
        "02-upstream-${_patchrel}.patch"
        "03-infinality-${pkgver}-${_patchrel}.patch")
#"02-ftsmooth-${pkgver}.patch"
sha1sums=('7b7460ef51a8fdb17baae53c6658fc1ad000a1c2'
          'd09d96c10020772533a4280cec2425bcd1a81fa2'
          'abf7a8f726ad6359533651a8942636880febf9f6'
          '0e9d1304858db2387d79cdded077f8ebc51125d4'
          '359d1fbbf4a207041222cd34a47d44084e9a1056')

prepare() {
  cd "freetype-${pkgver}"

  #"02-ftsmooth-${pkgver}.patch"
  patches=("01-freetype-${pkgver}-enable-valid.patch"
           "02-upstream-${_patchrel}.patch"
           "03-infinality-${pkgver}-${_patchrel}.patch")

  # infinality & post release fixes
  for patch in "${patches[@]}"; do
    patch -Np1 -i ${srcdir}/"${patch}"
  done
}

build() {
  cd "freetype-${pkgver}"

  ./configure \
    --prefix=/usr \
    --disable-static \
    --with-harfbuzz \
    --with-png

  make
}

check() {
  cd "freetype-${pkgver}"
  make -k check
}

package() {
  cd "freetype-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # freetype2 runtime settings
  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
  install -m755 "${srcdir}/infinality-settings.sh" \
    "${pkgdir}/etc/X11/xinit/xinitrc.d/infinality-settings.sh"

  sed -i "1i #!/bin/bash\n" \
    "${pkgdir}/etc/X11/xinit/xinitrc.d/infinality-settings.sh"
}
