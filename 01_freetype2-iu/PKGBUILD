# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: JIN Xiao-Yong <jinxiaoyong@gmail.com>
# Maintainer: bohoomil <@zoho.com>

pkgname=freetype2-infinality-ultimate
pkgver=2.5.5
pkgrel=1
_patchrel=2014.12.30
pkgdesc="TrueType font rendering library with Infinality patches and custom settings."
arch=('i686' 'x86_64')
changelog=CHANGELOG
license=('GPL' 'MIT')
groups=('infinality-bundle')
url="http://freetype.sourceforge.net"
depends=('zlib' 'bzip2' 'sh' 'xorg-xrdb' 'libpng' 'harfbuzz')
conflicts=('freetype2' 'freetype2-infinality')
provides=("freetype2=$pkgver" 'freetype2-infinality' 'freetype2-infinality-ultimate')
install='infinality.install'
backup=('etc/X11/xinit/xinitrc.d/infinality-settings')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        infinality-settings.sh
        "01-freetype-${pkgver}-enable-valid.patch"
        "02-ftsmooth-${pkgver}.patch"
        "04-infinality-${pkgver}-${_patchrel}.patch")
#"03-upstream-${_patchrel}.patch"
sha1sums=('7b7460ef51a8fdb17baae53c6658fc1ad000a1c2'
          'e6fc24261a645682a55764b6dccaaf57ff1580c8'
          'abf7a8f726ad6359533651a8942636880febf9f6'
          'fbd3b9ea3b94900b9818a8e9eb6cd6c0c4132188'
          '0f57a47247f212e9ed957c816c20a564d29a2606')

prepare() {
  cd "freetype-${pkgver}"

  patches=("01-freetype-${pkgver}-enable-valid.patch"
           "02-ftsmooth-${pkgver}.patch"
           "04-infinality-${pkgver}-${_patchrel}.patch")
           #"03-upstream-${_patchrel}.patch"

  # infinality & post release fixes
  for patch in "${patches[@]}"; do
    patch -Np1 -i ${srcdir}/"${patch}"
  done
}

build() {
  cd "freetype-${pkgver}"

  ./configure \
    --prefix=/usr \
    --disable-static \
    --with-harfbuzz \
    --with-png

  make
}

check() {
  cd "freetype-${pkgver}"
  make -k check
}

package() {
  cd "freetype-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -T "${srcdir}/infinality-settings.sh" \
    "${pkgdir}/etc/X11/xinit/xinitrc.d/infinality-settings"

  sed -i "1i #!/bin/bash\n" \
    "${pkgdir}/etc/X11/xinit/xinitrc.d/infinality-settings"
}
