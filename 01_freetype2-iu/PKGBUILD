# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: JIN Xiao-Yong <jinxiaoyong@gmail.com>
# Maintainer: bohoomil <@zoho.com>

pkgname=freetype2-infinality-ultimate
pkgver=2.5.3
pkgrel=15
_patchrel=2014.11.21
pkgdesc="TrueType font rendering library with Infinality patches and custom settings by bohoomil (infinality-bundle)."
arch=('i686' 'x86_64')
changelog=CHANGELOG
license=('GPL' 'MIT')
groups=('infinality-bundle')
url="http://freetype.sourceforge.net"
depends=('zlib' 'bzip2' 'sh' 'xorg-xrdb' 'libpng' 'harfbuzz')
conflicts=('freetype2' 'freetype2-infinality')
provides=("freetype2=$pkgver" 'freetype2-infinality' 'freetype2-infinality-ultimate')
install='infinality.install'
backup=('etc/X11/xinit/xinitrc.d/infinality-settings')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        infinality-settings.sh
        freetype-2.5.3-enable-valid.patch
        "upstream-${_patchrel}.patch"
        "infinality-2.5.3-${_patchrel}.patch")
sha1sums=('d3c26cc17ec7fe6c36f4efc02ef92ab6aa3f4b46'
          '8f602756c60310d0cbbd29c76b858547599be0a0'
          'abf7a8f726ad6359533651a8942636880febf9f6'
          'b8334216edba1c80b99766a1d23cf1ced20390f2'
          '8b2fa8bbebed1d39dfc4329c606f098a092cff77')

prepare() {
  cd "freetype-${pkgver}"

  patches=(freetype-2.5.3-enable-valid.patch
           "upstream-${_patchrel}.patch"
           "infinality-2.5.3-${_patchrel}.patch")

  # infinality & post release fixes
  for patch in "${patches[@]}"; do
    patch -Np1 -i ${srcdir}/"${patch}"
  done
}

build() {
  cd "freetype-${pkgver}"

  ./configure \
    --prefix=/usr \
    --disable-static \
    --with-harfbuzz \
    --with-png

  make
}

check() {
  cd "freetype-${pkgver}"
  make -k check
}

package() {
  cd "freetype-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -T "${srcdir}/infinality-settings.sh" \
    "${pkgdir}/etc/X11/xinit/xinitrc.d/infinality-settings"

  sed -i "1i #!/bin/bash\n" \
    "${pkgdir}/etc/X11/xinit/xinitrc.d/infinality-settings"
}
