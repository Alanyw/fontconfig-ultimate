# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: JIN Xiao-Yong <jinxiaoyong@gmail.com>
# Maintainer: bohoomil <@zoho.com>

# Installation order: freetype2-iu, fontconfig-iu, cairo-iu
pkgname=freetype2-infinality-ultimate
pkgver=2.5.3
pkgrel=2
pkgdesc="TrueType font rendering library with Infinality patches and custom settings by bohoomil (infinality-bundle)."
arch=(i686 x86_64)
license=('GPL' 'MIT')
groups=('infinality-bundle')
url="http://freetype.sourceforge.net"
depends=('zlib' 'bzip2' 'sh' 'xorg-xrdb' 'libpng')
conflicts=('freetype2' 'freetype2-infinality')
provides=("freetype2=$pkgver" 'freetype2-infinality' 'freetype2-infinality-ultimate')
install='infinality.install'
backup=('etc/profile.d/infinality-settings.sh')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        freetype-2.2.1-enable-valid.patch
        freetype-2.5.2-pkgconfig.patch
        infinality-2.5.2.patch
        infinality-settings.sh)

prepare() {
  cd "${srcdir}/freetype-${pkgver}"

  patches=(freetype-2.2.1-enable-valid.patch
           freetype-2.5.2-pkgconfig.patch
           infinality-2.5.2.patch)

  # infinality & post release fixes
  for patch in "${patches[@]}"; do
    patch -Np1 -i ${srcdir}/${patch}
  done
}

build() {
  cd "${srcdir}/freetype-${pkgver}"

  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/freetype-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/freetype-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -T "${srcdir}/infinality-settings.sh" "${pkgdir}/etc/profile.d/infinality-settings.sh"
}

md5sums=('d6b60f06bfc046e43ab2a6cbfd171d65'
         '214119610444c9b02766ccee5e220680'
         '4fd922532b451d862a5374c2cc801581'
         'ac23b2f92d084d2fff1c3766b9c955dc'
         'b4e48d4e3d762593973a9b8da25b91a7')
