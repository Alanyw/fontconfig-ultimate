# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-lato-ibx
pkgver=2.015
pkgrel=2
depends=('fontconfig')
pkgdesc="Lato is a sanserif type­face family designed in the Summer 2010 by Warsaw-based designer Łukasz Dziedzic."
url="http://www.latofonts.com/"
arch=('any')
groups=('infinality-bundle-fonts-extra')
license=('custom:OFL')
conflicts=('ttf-lato' 't1-lato-ibx' 'otf-lato-ib')
replaces=('t1-lato-ibx' 'otf-lato-ib')
source=("http://www.latofonts.com/download/Lato2OFL.zip"
        http://bohoomil.com/src/lato-thin-${pkgver}-fixed.tar.xz{,.sig}
        45-lato.conf
        90-tt-lato.conf)
validgpgkeys=('A9244FB5E93F11F0E975337FAE6866C7962DDE58')
sha1sums=('1b29ec9a8a1c151ff0f2e9f49d5544ddce217604'
          '3069f1d9d0f43d0c116aa0f4dc62371de6661058'
          'SKIP'
          '86f9ff2da81ba10f64e6747fdd8d27231da28d2b'
          'a84058cb3051d858221a7a0eefeea18db24306c5')

package(){
  cd Lato2OFL

  install -D -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"

lato1=(Lato-Black.ttf Lato-BlackItalic.ttf
       Lato-Bold.ttf Lato-BoldItalic.ttf
       Lato-Heavy.ttf Lato-HeavyItalic.ttf
       Lato-Regular.ttf Lato-Italic.ttf
       Lato-Light.ttf Lato-LightItalic.ttf
       Lato-Medium.ttf Lato-MediumItalic.ttf
       Lato-Semibold.ttf Lato-SemiboldItalic.ttf)

  for fnt in "${lato1[@]}"; do
    install -m644 "$fnt" "${pkgdir}"/usr/share/fonts/"${pkgname}"
  done

  # fixed weights
  install -m644 "${srcdir}/lato-thin-${pkgver}-fixed/"*.ttf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -D -m644 45-lato.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-lato.conf
  install -D -m644 90-tt-lato.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-lato.conf

  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-lato.conf .
  ln -s ../conf.avail/90-tt-lato.conf .
}
