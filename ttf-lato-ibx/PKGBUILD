# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-lato-ibx
pkgver=2.007
pkgrel=5
depends=('fontconfig')
pkgdesc="Lato is a sanserif type­face family designed in the Summer 2010 by Warsaw-based designer Łukasz Dziedzic."
url="http://www.latofonts.com/"
arch=('any')
groups=('infinality-bundle-fonts-extra')
license=('OFL-1.1')
conflicts=('ttf-lato' 't1-lato-ibx' 'otf-lato-ib')
replaces=('t1-lato-ibx' 'otf-lato-ib')
install=$pkgname.install
source=("http://www.latofonts.com/download/Lato2OFL.zip"
        45-lato.conf
        90-tt-lato.conf)
sha1sums=('a66cda6cef79d80a9266eefb65d0da88eaa8c09c'
          '86f9ff2da81ba10f64e6747fdd8d27231da28d2b'
          'a84058cb3051d858221a7a0eefeea18db24306c5')

package(){
  cd "${srcdir}"/Lato2OFL

  install -m755 -d "${pkgdir}"/usr/share/doc/"${pkgname}"
  install -m644 README.txt "${pkgdir}"/usr/share/doc/"${pkgname}"
  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 Lato-{Black,Bold,Heavy,Light,Medium,Semibold,Thin}*.ttf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 Lato-{Regular,Italic}.ttf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../45-lato.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-lato.conf
  install -m644 ../90-tt-lato.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-lato.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-lato.conf .
  ln -s ../conf.avail/90-tt-lato.conf .
}
