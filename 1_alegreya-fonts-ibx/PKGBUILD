# Maintainer: bohoomil <@zoho.com>

pkgname=alegreya-fonts-ibx
pkgname=('otf-alegreya-ibx' 'otf-alegreya-sans-ibx')
pkgver=1.003
pkgrel=4
depends=('fontconfig')
pkgdesc="Alegreya and Alegreya Sans are modern font families designed by Juan Pablo del Peral."
url="http://www.fontsquirrel.com/fonts/alegreya"
arch=('any')
groups=('infinality-bundle-fonts-extra')
license=('OFL-1.1')
install=$pkgname.install
# http://www.fontsquirrel.com/
source=(alegreya.zip
        alegreya-sans.zip
        45-alegreya.conf
        45-alegreya-sans.conf
        90-non-tt-alegreya.conf
        90-non-tt-alegreya-sans.conf)
sha1sums=('3a60711e43440624063c6e8922aac99312207634'
          '7a0e62bb27d97a6fd02e58e3f76ca2fd9a34ce44'
          'db08107ef08332b400ba3fcc5eb78fe0cd8ebebe'
          '8454c43dce5f6df1ed2bc7fa8c1a1aa8223766a5'
          '1ed2bba89da54bfcb12806f2e0b900ebc8b63fd8'
          '5b3fe46dd4d5c2ffcf72f6311023cd8570db734f')

package_otf-alegreya-ibx(){
  pkgdesc="Alegreya is a serif typeface originally intended for literature."
  install=$pkgname.install

  cd "${srcdir}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 'SIL Open Font License.txt' \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -Dm644 Alegreya-{Black,BlackItalic,Bold,BoldItalic,Italic,Regular}.otf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -Dm644 AlegreyaSC-{Black,BlackItalic,Bold,BoldItalic,Italic,Regular}.otf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-alegreya.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-alegreya.conf
  install -m644 90-non-tt-alegreya.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-alegreya.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-alegreya.conf .
  ln -s ../conf.avail/90-non-tt-alegreya.conf .
}

package_otf-alegreya-sans-ibx(){
  pkgdesc="Alegreya Sans is a humanist sans serif family with a calligraphic feeling that conveys a dynamic and varied rhythm."
  install=$pkgname.install

  cd "${srcdir}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 'SIL Open Font License.txt' \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -Dm644 AlegreyaSans-{Black,BlackItalic,Bold,BoldItalic,ExtraBold,ExtraBoldItalic,Italic,Light,LightItalic,Medium,MediumItalic,Regular,Thin,ThinItalic}.otf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -Dm644 AlegreyaSansSC-{Black,BlackItalic,Bold,BoldItalic,ExtraBold,ExtraBoldItalic,Italic,Light,LightItalic,Medium,MediumItalic,Regular,Thin,ThinItalic}.otf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"


  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-alegreya-sans.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-alegreya-sans.conf
  install -m644 90-non-tt-alegreya-sans.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-alegreya-sans.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-alegreya-sans.conf .
  ln -s ../conf.avail/90-non-tt-alegreya-sans.conf .
}
