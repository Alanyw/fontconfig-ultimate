# Maintainer: bohoomil <@zoho.com>

pkgname=heuristica-ib
pkgname=('otf-heuristica-ib' 'ttf-heuristica-ibx')
pkgver=1.0.2
pkgrel=1
depends=('fontconfig')
pkgdesc="Heuristica is an extended version of Adobe Utopia font."
url="http://code.google.com/p/evristika/"
arch=('any')
license=('OFL-1.1')
groups=('infinality-bundle-fonts')
source=("https://downloads.sourceforge.net/project/heuristica/heuristica-otf-${pkgver}.tar.xz"
        "https://downloads.sourceforge.net/project/heuristica/heuristica-ttf-${pkgver}.tar.xz"
        45-heuristica.conf
        90-non-tt-heuristica.conf
        90-tt-heuristica.conf)
sha1sums=('cbfcf2f2e51735403ed2bca1c4eed83ff01a539f'
          '7b233f1f66816e0f023f57d7385cf1d40c04f2ec'
          '902385dcf1c3fc3f66bfeabba4589ecdb90593a7'
          'b37246ab81fdf615d0640cb081d225b2890f8b0a'
          '710edd28c4f01f9449f94413a1d372c81aaf783e')

package_otf-heuristica-ib(){
  pkgdesc="Heuristica is an extended version of Adobe Utopia font. OpenType version."
  install=$pkgname.install
  conflicts=('otf-heuristica' 't1-heuristica-ib' 'ttf-heuristica-ibx')
  replaces=('otf-heuristica')
  provides=('t1-adobe-utopia-ib')

  cd "${srcdir}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d ${pkgdir}/usr/share/fonts/"${pkgname}"
  install -m644 *.otf ${pkgdir}/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-heuristica.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-heuristica.conf
  install -m644 90-non-tt-heuristica.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-heuristica.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-heuristica.conf .
  ln -s ../conf.avail/90-non-tt-heuristica.conf .
}

package_ttf-heuristica-ibx(){
  pkgdesc="Heuristica is an extended version of Adobe Utopia font. TrueType version."
  conflicts=('otf-heuristica' 't1-heuristica-ib' 'otf-heuristica-ib')
  provides=('t1-adobe-utopia-ib')
  install=$pkgname.install
  cd "${srcdir}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d ${pkgdir}/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf ${pkgdir}/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-heuristica.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-heuristica.conf
  install -m644 90-tt-heuristica.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-heuristica.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-heuristica.conf .
  ln -s ../conf.avail/90-tt-heuristica.conf .
}
