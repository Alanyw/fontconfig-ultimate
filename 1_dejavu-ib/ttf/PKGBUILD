# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>
# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-dejavu-ib
pkgver=2.34
pkgrel=12
pkgdesc="Font family based on the Bitstream Vera Fonts with a wider range of characters. Re-packed for infinality-bundle-fonts. TrueType version."
url="http://dejavu-fonts.org/wiki/Main_Page"
arch=('any')
depends=('fontconfig')
groups=('infinality-bundle-fonts')
conflicts=('ttf-dejavu' 't1-dejavu-ib')
provides=('ttf-font' 'ttf-dejavu' 'dejavu-ib')
replaces=('ttf-dejavu')
license=('custom')
source=(http://downloads.sourceforge.net/project/dejavu/dejavu/${pkgver}/dejavu-fonts-ttf-${pkgver}.tar.bz2
        45-dejavu.conf
        90-tt-dejavu.conf)
sha1sums=('33fa825aa1dd44ce23a11af92b3078e978ff513d'
          '4072385eb6d18754f932bda5634da259ab875a1b'
          'a6332f56d9d95636e9ac99643b3a241ba08f3499')

package() {
  install=${pkgname}.install

  cd "${srcdir}"/dejavu-fonts-ttf-${pkgver}
  install -dm755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 ttf/*.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -Dm644 LICENSE \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -dm755 "${pkgdir}"/etc/fonts/conf.avail
  install -dm755 "${pkgdir}"/etc/fonts/conf.d
  install -m644 "${srcdir}"/45-dejavu.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-dejavu.conf
  install -m644 "${srcdir}/"90-tt-dejavu.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-dejavu.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-dejavu.conf .
  ln -s ../conf.avail/90-tt-dejavu.conf .
}

