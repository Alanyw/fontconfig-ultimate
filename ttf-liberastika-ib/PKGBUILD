# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-liberastika-ib
pkgver=1.1.4
pkgrel=3
depends=('fontconfig')
pkgdesc="Liberastika is a Liberation Sans derivative typeface with re-designed and enhanced Cyrillic section."
url="http://code.google.com/p/liberastika/"
arch=('any')
groups=('infinality-bundle-fonts')
conflicts=('ttf-liberastika')
license=('GPL')
source=(http://liberastika.googlecode.com/files/liberastika-ttf-${pkgver}.tar.xz
        45-liberastika.conf
        90-tt-liberastika.conf)
sha1sums=('9de393a8bde6f503c94f8bd9367339cf6c33d822'
          '232668931aa4e053a59a815cf76d1dbc5dc9032a'
          'be832bf9a5c46fb416098d019e8c75eefdb5da4a')

package(){

  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -D -m644 45-liberastika.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-liberastika.conf
  install -D -m644 90-tt-liberastika.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-liberastika.conf

  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-liberastika.conf .
  ln -s ../conf.avail/90-tt-liberastika.conf .
}
