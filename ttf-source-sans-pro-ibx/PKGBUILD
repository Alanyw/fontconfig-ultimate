# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-source-sans-pro-ibx
pkgver=1.050
pkgrel=3
depends=('fontconfig')
pkgdesc="A set of OpenType fonts designed for user interfaces. TrueType version."
url="http://sourceforge.net/projects/sourcesans.adobe/"
arch=('any')
license=('OFL-1.1')
groups=('infinality-bundle-fonts-extra')
replaces=('adobe-source-sans-pro-fonts')
conflicts=('adobe-source-sans-pro-fonts' 'otf-source-sans-pro' 'ttf-source-sans-pro' 't1-source-sans-pro-ibx')
install=$pkgname.install
source=("http://downloads.sourceforge.net/project/sourcesans.adobe/SourceSansPro_FontsOnly-${pkgver}.zip"
        90-tt-source-sans-pro.conf)
sha1sums=('f3af1201c80a86f1dd6e045a565c96bdd12d445f'
          'af66e90f1e5b854ec80dae2887a900b6b4339cc2')

package(){
  cd "${srcdir}"/SourceSansPro_FontsOnly-${pkgver}

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 TTF/SourceSansPro-{Black,BlackIt,Bold,BoldIt,It,Light,LightIt,Regular,Semibold,SemiboldIt}.ttf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 90-tt-source-sans-pro.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-source-sans-pro.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/90-tt-source-sans-pro.conf .
}
