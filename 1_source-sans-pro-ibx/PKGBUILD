# Maintainer: bohoomil <@zoho.com>

pkgname=source-sans-pro-ibx
pkgname=('otf-source-sans-pro-ibx' 'ttf-source-sans-pro-ibx')
pkgver=2.010
pkgrel=1
depends=('fontconfig')
pkgdesc="A set of OpenType fonts designed for user interfaces."
url="http://www.adobe.com/"
arch=('any')
license=('OFL-1.1')
groups=('infinality-bundle-fonts-extra')
source=("http://downloads.sourceforge.net/project/sourcesans.adobe/SourceSansPro_FontsOnly-${pkgver}roman-1.065-italic.zip"
        45-source-sans-pro.conf
        90-tt-source-sans-pro.conf
        90-non-tt-source-sans-pro.conf)
sha1sums=('5ec478abfbf330a858c6e87f664e3bc52dd3081e'
          'f692d46268b32bc62cb5a4dc515f9a427daf3d8c'
          '94f54a3f68ee1706db1d0a017b59b5c620fe45cd'
          '5dc78c30e7ba06aaae72b80241e93fc857e2c9ec')

package_otf-source-sans-pro-ibx(){
  pkgdesc="A set of OpenType fonts designed for user interfaces. OpenType version."
  conflicts=('adobe-source-sans-pro-fonts' 'otf-source-sans-pro' 'ttf-source-sans-pro' 't1-source-sans-pro-ibx' 'ttf-source-sans-pro-ibx')
  install=$pkgname.install

  cd "${srcdir}"/source-sans-pro-${pkgver}R-ro-1.065R-it

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 LICENSE.txt \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 OTF/*.otf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-source-sans-pro.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-source-sans-pro.conf
  install -m644 90-non-tt-source-sans-pro.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-source-sans-pro.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-source-sans-pro.conf .
  ln -s ../conf.avail/90-non-tt-source-sans-pro.conf .
}

package_ttf-source-sans-pro-ibx(){
  pkgdesc="A set of OpenType fonts designed for user interfaces. TrueType version."
  conflicts=('adobe-source-sans-pro-fonts' 'otf-source-sans-pro' 'ttf-source-sans-pro' 't1-source-sans-pro-ibx' 'otf-source-sans-pro-ibx')
  install=$pkgname.install

  cd "${srcdir}"/source-sans-pro-${pkgver}R-ro-1.065R-it

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 LICENSE.txt \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 TTF/SourceSansPro-{Black,BlackIt,Bold,BoldIt,It,Light,LightIt,Regular,Semibold,SemiboldIt}.ttf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-source-sans-pro.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-source-sans-pro.conf
  install -m644 90-tt-source-sans-pro.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-source-sans-pro.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-source-sans-pro.conf .
  ln -s ../conf.avail/90-tt-source-sans-pro.conf .
}
