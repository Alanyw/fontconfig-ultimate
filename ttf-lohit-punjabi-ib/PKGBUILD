# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-lohit-punjabi-ib
_name=lohit-punjabi-ttf
pkgver=2.5.3
pkgrel=7
depends=('fontconfig')
pkgdesc="Lohit Punjabi font."
url="https://fedorahosted.org/lohit/"
arch=('any')
conflicts=('lohit-fonts')
groups=('infinality-bundle-fonts')
license=('custom:OFL')
source=("https://fedorahosted.org/releases/l/o/lohit/${_name}-${pkgver}.tar.gz"
        40-lohit-punjabi.conf
        66-lohit-punjabi.conf
        90-tt-lohit-punjabi.conf)
sha1sums=('c43c7c9a3ef24b78187bd157ee9ac46559816959'
          '7ac5786dd5f78bf71f492b4e93fb77b471f1256c'
          '29107f30f462a7d3c5d7fadcceedb15660620314'
          'c4878968c17dab5fa06af6c0cf50a22a9742d9bd')

package(){
  cd "${_name}-${pkgver}"

  install -D -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -D -m644 40-lohit-punjabi.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-lohit-punjabi.conf
  install -D -m644 66-lohit-punjabi.conf \
    "${pkgdir}"/etc/fonts/conf.avail/66-lohit-punjabi.conf
  install -D -m644 90-tt-lohit-punjabi.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-lohit-punjabi.conf

  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-lohit-punjabi.conf .
  ln -s ../conf.avail/66-lohit-punjabi.conf .
  ln -s ../conf.avail/90-tt-lohit-punjabi.conf .
}
