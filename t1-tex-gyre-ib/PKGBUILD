# Maintainer: bohoomil <@zoho.com>

pkgname=t1-tex-gyre-ib
pkgver=2.005
pkgrel=2
depends=('fontconfig')
pkgdesc="The TeX Gyre (TG) Collection of Fonts. Type1 versions."
groups=('infinality-bundle-fonts')
url="http://www.gust.org.pl/projects/e-foundry/tex-gyre/"
arch=('any')
license=('GFL')
conflicts=('otf-texgyre' 'tex-gyre-fonts')
install=$pkgname.install
source=("http://www.gust.org.pl/projects/e-foundry/tex-gyre/whole/tg-2.005bas.zip")

package(){
  cd "${srcdir}"

  install -m755 -d "${pkgdir}"/usr/share/doc/"${pkgname}"
  install -m644 doc/fonts/tex-gyre/README-TeX-Gyre-*.txt \
    "${pkgdir}"/usr/share/doc/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 doc/fonts/tex-gyre/GUST-FONT-LICENSE.txt \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 fonts/afm/public/tex-gyre/*.afm \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 fonts/type1/public/tex-gyre/*.{pfb,pfm} \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"
}

md5sums=('acdfa942368f38b52fcf56c94b7e2289')
