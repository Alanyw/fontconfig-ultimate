# Contributor: Firmicus <francois.archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: bohoomil <@zoho.com>

pkgname=t1-urw-fonts-ib
_pkgname=urw-core35-fonts
pkgver=20150122
pkgrel=2
pkgdesc='Standard Ghostscript Type1 fonts from URW for infinality-bundle-fonts.'
url='http://sourceforge.net/projects/gs-fonts/'
arch=('any')
license=('GPL2')
depends=('fontconfig')
conflicts=('gsfonts')
provides=('gsfonts' "$pkgname_org")
groups=('infinality-bundle-fonts')
install=$pkgname.install
source=("git://git.ghostscript.com/${_pkgname}.git#commit=8ddd6b336d95c43279efcc935cbe4180958b8a31"
        45-urw-fonts.conf
        90-non-tt-urw-fonts.conf)
sha1sums=('SKIP'
          '8f26b0f1f3703abd096f73f3b51acf427d1cee3d'
          '37cc6b72ffe34bea5b999399432ce7e1b12a43a3')

package() {
  cd "${srcdir}/${_pkgname}"

  install -d "${pkgdir}"/usr/share/fonts/${pkgname}
  install -m644 *.{pfb,pfm,afm} "${pkgdir}"/usr/share/fonts/${pkgname}

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../45-urw-fonts.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-urw-fonts.conf
  install -m644 ../90-non-tt-urw-fonts.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-urw-fonts.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-urw-fonts.conf .
  ln -s ../conf.avail/90-non-tt-urw-fonts.conf .
}
