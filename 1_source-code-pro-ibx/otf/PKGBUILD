# Maintainer: bohoomil <@zoho.com>

pkgname=otf-source-code-pro-ibx
pkgver=1.017
pkgrel=5
depends=('fontconfig')
pkgdesc="A set of OpenType fonts designed for user interfaces. OpenType version."
url="http://www.adobe.com/"
arch=('any')
conflicts=('adobe-source-sans-pro-fonts' 'otf-source-sans-pro' 'ttf-source-sans-pro' 't1-source-code-pro-ibx' 'ttf-source-code-pro')
license=('custom:OFL')
groups=('infinality-bundle-fonts-extra')
install=$pkgname.install
source=("https://github.com/adobe-fonts/source-code-pro/archive/${pkgver}R.tar.gz"
        45-source-code-pro.conf
        90-non-tt-source-code-pro.conf)
sha1sums=('badce7cc053e7e943636920140fd249676585b36'
          'd20feb494116f95e4a35ad40f625a94f9a3bbcb2'
          '287ef2255289182912eb2daa16810bd87d7100b1')

package(){
  cd "source-code-pro-${pkgver}R"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 LICENSE.txt \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 OTF/*.otf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-source-code-pro.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-source-code-pro.conf
  install -m644 90-non-tt-source-code-pro.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-source-code-pro.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-source-code-pro.conf .
  ln -s ../conf.avail/90-non-tt-source-code-pro.conf .
}
