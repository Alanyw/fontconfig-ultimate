# Contributor: Rick W. Chen <stuffcorpse@archlinux.us>
# Contributor: bohoomil <@zoho.com>

pkgname=otf-linux-libertine-ibx
pkgver=5.3.0
pkgrel=3
pkgdesc="Linux Libertine and Linux Biolinum fonts."
depends=('fontconfig')
arch=(any)
url="http://www.linuxlibertine.org/"
license=('GPL' 'OFL')
groups=('infinality-bundle-fonts-extra')
replaces=('t1-libertine-ibx' 'otf-libertine-ibx')
conflicts=('ttf-linux-libertine' 'ttf-linux-libertine-g' 'texlive-libertine-type1' 'texlive-biolinum-type1' 'ttf-libertine-ibx' 't1-libertine-ibx')
source=("http://mirrors.ctan.org/fonts/libertine.zip"
        90-non-tt-linux-libertine.conf)
install=${pkgname}.install

package() {
  cd "${srcdir}"/libertine
  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 doc/LICENCE.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/GPL.txt

  install -d -m755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 opentype/*.otf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -dm755 "${pkgdir}"/etc/fonts/conf.avail
  install -dm755 "${pkgdir}"/etc/fonts/conf.d
  install -m644 90-non-tt-linux-libertine.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-linux-libertine.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/90-non-tt-linux-libertine.conf .
}

md5sums=('ea5b97d6abb300fc2a4d6e1503bbfc0c'
         'aad4a9be1683fc01c881d7517e074412')
