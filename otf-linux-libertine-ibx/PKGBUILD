# Contributor: Rick W. Chen <stuffcorpse@archlinux.us>
# Contributor: bohoomil <@zoho.com>

pkgname=otf-linux-libertine-ibx
pkgver=5.3.0
pkgrel=4
pkgdesc="Linux Libertine and Linux Biolinum fonts."
depends=('fontconfig')
arch=(any)
url="http://www.linuxlibertine.org/"
license=('GPL' 'OFL')
groups=('infinality-bundle-fonts-extra')
replaces=('t1-libertine-ibx' 'otf-libertine-ibx')
conflicts=('ttf-linux-libertine' 'ttf-linux-libertine-g' 'texlive-libertine-type1' 'texlive-biolinum-type1' 'ttf-libertine-ibx' 't1-libertine-ibx')
install=${pkgname}.install
source=("http://mirrors.ctan.org/fonts/libertine.zip"
        90-non-tt-linux-libertine.conf)
sha1sums=('e1e3d9e0eff1258d34fc4a5ee4e2dcef8720fb28'
          '1c443c4aca2b0b6c9b6f8ee1ba9ecc14d4b09a0d')

package() {
  cd "${srcdir}"/libertine
  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 doc/LICENCE.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/GPL.txt

  install -d -m755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 opentype/*.otf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -dm755 "${pkgdir}"/etc/fonts/conf.avail
  install -dm755 "${pkgdir}"/etc/fonts/conf.d
  install -m644 90-non-tt-linux-libertine.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-linux-libertine.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/90-non-tt-linux-libertine.conf .
}

