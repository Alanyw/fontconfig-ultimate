# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Urs Wolfer <uwolfer @ fwo.ch>
# Mainainer: bohoomil <@zoho.com>

pkgname=ttf-droid-ib
pkgver=20121017
pkgrel=7
pkgdesc="General-purpose fonts released by Google as part of Android. Includes additional monospaced variants with dotted and slashed zeros."
arch=('any')
license=('apache')
url="http://code.google.com/android/"
depends=('fontconfig')
groups=('infinality-bundle-fonts')
conflicts=('ttf-droid' 'ttf-droid-monovar' 'ttf-droid-monovar-ib')
replaces=('ttf-droid' 'ttf-droid-monovar' 'ttf-droid-monovar-ib')
provides=('ttf-font' 'ttf-droid')
install=$pkgname.install
source=(ftp://ftp.archlinux.org/other/community/ttf-droid/ttf-droid-20121017.tar.xz
        http://www.cosmix.org/software/files/DroidSansMonoSlashed.zip
        http://www.cosmix.org/software/files/DroidSansMonoDotted.zip
        45-droid.conf
        65-ttf-droid-sans.conf
        65-ttf-droid-serif.conf
        90-tt-droid.conf)

package(){
  cd "${srcdir}"/google-droid-fonts-"${pkgver}"
  install -d -m755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m 644 ../*.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -d "${pkgdir}"/etc/fonts/conf.avail
  install -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-droid.conf "${pkgdir}"/etc/fonts/conf.avail
  install -m644 65-ttf-droid-sans.conf "${pkgdir}"/etc/fonts/conf.avail
  install -m644 65-ttf-droid-serif.conf "${pkgdir}"/etc/fonts/conf.avail
  install -m644 90-tt-droid.conf "${pkgdir}"/etc/fonts/conf.avail

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-droid.conf .
  ln -s ../conf.avail/65-ttf-droid-sans.conf .
  ln -s ../conf.avail/65-ttf-droid-serif.conf .
  ln -s ../conf.avail/90-tt-droid.conf .
}
sha1sums=('4190b9c3a75766df2e4a8a1a38c4c1722c63e41b'
          '50c4ea8f89e9559086e63389eee2da8c8636500e'
          '842a341b1b79119418d26f406662a18d2264862e'
          '7aa504d19793ac0feda1882d1504fe0cd5cf7654'
          'a6d3cc79db9e3cc01aedf599691314499edae02d'
          '1d540e1d02740a3e2cfe56c6197dad8d317193ef'
          '0cc0afffe885f3899b02387d0979344af60888c2')

