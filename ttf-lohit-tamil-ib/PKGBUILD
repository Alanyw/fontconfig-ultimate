# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-lohit-tamil-ib
_src_pkg_1=lohit-tamil-ttf
_src_pkg_2=lohit-tamil-classical-ttf
pkgver=2.5.3
pkgrel=3
depends=('fontconfig')
pkgdesc="Lohit Tamil fonts."
url="https://fedorahosted.org/lohit/"
arch=('any')
conflicts=('lohit-fonts')
groups=('infinality-bundle-fonts')
license=('OFL-1.1')
install=$pkgname.install
source=("https://fedorahosted.org/releases/l/o/lohit/${_src_pkg_1}-${pkgver}.tar.gz"
        "https://fedorahosted.org/releases/l/o/lohit/${_src_pkg_2}-${pkgver}.tar.gz"
        '66-lohit-tamil.conf')
sha1sums=('6e2f24dd49b31618108989bdb8d61db769ceae48'
          'eec360e6d7d85818932b84e04f431e9da5f79bd7'
          '237665611fc6cdb208fb8971628800ce2091ff85')

package(){
  cd "${srcdir}"/"${_src_pkg_1}-${pkgver}"
  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"/"${_src_pkg_2}-${pkgver}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -dm755 "${pkgdir}"/etc/fonts/conf.avail
  install -dm755 "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../66-lohit-tamil.conf \
    "${pkgdir}"/etc/fonts/conf.avail/66-lohit-tamil.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/66-lohit-tamil.conf .
}
