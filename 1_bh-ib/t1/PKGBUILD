# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: bohoomil <@zoho.com>

pkgname=t1-bh-ib
_basename=font-bh-type1
pkgver=1.0.3
pkgrel=3
pkgdesc="Luxi Sans, Serif and Mono families by Bigelow & Holmes. Type 1 version."
url="http://xorg.freedesktop.org/"
arch=(any)
depends=('fontconfig')
groups=('infinality-bundle-fonts')
license=('custom')
conflicts=('font-bh-ttf' 'luximono' 'ttf-bh-ib')
provides=('font-bh-ttf')
source=(${url}/releases/individual/font/font-bh-type1-1.0.3.tar.bz2
        45-luxi.conf
        90-non-tt-luxi.conf)
sha1sums=('69ff038d38015cd305a4cd4d1a921fe3bd08bbde'
          '8fdf9fc799fe19a9ef492a6ad442cb5770f8431a'
          'd269bad6719f1d790dd4955a0805b5778661be8a')

package() {
  cd "${_basename}-${pkgver}"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  install -m755 -d "${pkgdir}/usr/share/fonts/${pkgname}"
  install -m644 *.afm "${pkgdir}/usr/share/fonts/${pkgname}"
  install -m644 *.pfa "${pkgdir}/usr/share/fonts/${pkgname}"

  cd "${srcdir}"
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-luxi.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-luxi.conf
  install -m644 90-non-tt-luxi.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-luxi.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-luxi.conf .
  ln -s ../conf.avail/90-non-tt-luxi.conf .
}

