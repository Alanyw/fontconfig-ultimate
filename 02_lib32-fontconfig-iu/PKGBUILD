# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: bohoomil <@zoho.com>

_basename=fontconfig
pkgname=lib32-$_basename-infinality-ultimate
pkgver=2.11.1
pkgrel=5
_master_name=fontconfig-ultimate-master
pkgdesc="A library for configuring and customizing font access (32-bit, infinality-bundle)"
arch=(x86_64)
url="http://www.fontconfig.org/release/"
license=('custom' 'MIT')
groups=('infinality-bundle-multilib')
depends=('lib32-expat' 'lib32-freetype2-infinality-ultimate' $_basename-infinality-ultimate)
makedepends=('gcc-multilib' 'lib32-expat' 'gperf' 'python2-lxml')
options=('!libtool')
provides=('lib32-fontconfig')
conflicts=('lib32-fontconfig')
install="lib32-fontconfig.install"
source=(http://www.fontconfig.org/release/${_basename}-${pkgver}.tar.bz2
        master.tar.gz::https://github.com/bohoomil/fontconfig-ultimate/archive/master.tar.gz)
sha1sums=('08565feea5a4e6375f9d8a7435dac04e52620ff2'
          'c467c14548e38722b8633b84d50211073008f8c0')

prepare() {

  # copy fontconfig-ib patches & stuff
  cd "${srcdir}/${_master_name}"

  cp -r fontconfig_patches/00-upstream_2.11.93.patch "${srcdir}/${_basename}-${pkgver}"
  cp -r fontconfig_patches/06-missing_mans.patch "${srcdir}/${_basename}-${pkgver}"

  # prepare src
  cd "${srcdir}/${_basename}-${pkgver}"

  # upstream fixes
  patch -Np1 -i 00-upstream_2.11.93.patch
  patch -Np1 -i 06-missing_mans.patch

  aclocal
  libtoolize -f
  automake -afi
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${_basename}-${pkgver}"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --disable-docs \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts \
    --libdir=/usr/lib32

  make
}

check() {
  cd "${_basename}-${pkgver}"
  make -k check
}

package() {
  cd "${_basename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/{etc,usr/{include,share}}
  find "${pkgdir}/usr/bin" -not -type d -not -name fc-cache -delete
  mv "${pkgdir}"/usr/bin/fc-cache{,-32}
  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s ${_basename} "${pkgdir}/usr/share/licenses/${pkgname}"
}
