# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: bohoomil <@zoho.com>

_basename=fontconfig
pkgname=lib32-$_basename-infinality-ultimate
pkgver=2.11.1
pkgrel=7
_master_name=fontconfig-ultimate-master
pkgdesc="A library for configuring and customizing font access (32-bit, infinality-bundle,  git version, r1780)"
arch=(x86_64)
url="http://www.fontconfig.org/release/"
license=('custom' 'MIT')
groups=('infinality-bundle-multilib')
depends=('lib32-expat' 'lib32-freetype2-infinality-ultimate' $_basename-infinality-ultimate)
makedepends=('gcc-multilib' 'lib32-expat' 'gperf' 'python2-lxml')
options=('!libtool')
provides=('lib32-fontconfig')
conflicts=('lib32-fontconfig')
install="lib32-fontconfig.install"

source=("http://bohoomil.com/src/${_basename}-git-r1780.tar.bz2"{,.sig}
        "http://bohoomil.com/src/${_basename}-ultimate-git.tar.bz2"{,.sig})
sha1sums=('d9e4f7a098355caf9e89ad080532ff098902f5da'
          'SKIP'
          '8cf389e512bbcc4baeab0f1d03a5f0e469b82139'
          'SKIP')

prepare() {

  # copy fontconfig-ib patches & stuff
  cd "${srcdir}/${_basename}-ultimate-git"

  cp -r fontconfig_patches/06-missing_mans.patch "${srcdir}/${_basename}"

  # prepare src
  cd "${srcdir}/${_basename}"

  # fix missing manuals fixes
  patch -Np1 -i 06-missing_mans.patch

  aclocal
  libtoolize -f
  automake -afi
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${_basename}"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --disable-docs \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts \
    --libdir=/usr/lib32

  make
}

check() {
  cd "${_basename}"
  make -k check
}

package() {
  cd "${_basename}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/{etc,usr/{include,share}}
  find "${pkgdir}/usr/bin" -not -type d -not -name fc-cache -delete
  mv "${pkgdir}"/usr/bin/fc-cache{,-32}
  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s ${_basename} "${pkgdir}/usr/share/licenses/${pkgname}"
}
