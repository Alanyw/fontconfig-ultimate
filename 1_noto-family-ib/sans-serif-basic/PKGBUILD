# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-noto-family-ib
pkgname=('ttf-noto-sans-ib' 'ttf-noto-serif-ib')
pkgver=2015.04.20
pkgrel=1
pkgdesc="Noto Sans family."
url="http://www.google.com/get/noto/"
arch=('any')
groups=('infinality-bundle-fonts')
license=('Apache')
source=(http://storage.googleapis.com/noto-website/pkgs/Noto-hinted.zip
        40-noto-arabic.conf
        45-noto-sans.conf
        45-noto-serif.conf
        90-non-tt-noto-sans-cjk.conf
        90-tt-noto-sans.conf
        90-tt-noto-serif.conf)
sha1sums=('b69866fb0a0380ab2d9877ab6be5761c3f814c32'
          '5c3c4b13db2ae3ac9f45f14d5129782fc33845e1'
          '1cf1ae4dbf940a25724ce479fe2ee55039a7ebe6'
          'e20c836813721d8bf53e07f6fced4a4639a3c238'
          'fd6fabab03f187fdd8eeabd655d75451347c28da'
          '52e6c6ea3a7b5ca5f7e6d9017ac0cd954d2ef6de'
          '017ca1ca6f041035c2dbaa2c7b8d36725e881534')

package_ttf-noto-sans-ib() {
  pkgdesc="Noto Sans family: Latin and basic non-Latin set."
  depends=('fontconfig')
  conflicts=('ttf-noto' 'ttf-noto-svn' 'ttf-google-webfonts-git')
  install=$pkgname.install

  cd "${srcdir}"

  _sans=(NotoSans-Bold.ttf NotoSans-BoldItalic.ttf NotoSans-Italic.ttf NotoSans-Regular.ttf)

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"

  for fnt in "${_sans[@]}"; do
    install -m644 "$fnt" "${pkgdir}"/usr/share/fonts/"${pkgname}"
  done

  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m644 45-noto-sans.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-noto-sans.conf
  install -m644 90-tt-noto-sans.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-noto-sans.conf
  install -m644 40-noto-arabic.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-noto-arabic.conf
  install -m644 90-non-tt-noto-sans-cjk.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-noto-sans-cjk.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-noto-arabic.conf .
  ln -s ../conf.avail/45-noto-sans.conf .
  ln -s ../conf.avail/90-non-tt-noto-sans-cjk.conf .
  ln -s ../conf.avail/90-tt-noto-sans.conf .
}

package_ttf-noto-serif-ib() {
  pkgdesc="Noto Serif family: Latin and basic non-Latin set."
  depends=('fontconfig')
  install=$pkgname.install

  cd "${srcdir}"

  _serif=(NotoSerif-Bold.ttf NotoSerif-BoldItalic.ttf NotoSerif-Italic.ttf NotoSerif-Regular.ttf)

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"

  for fnt in "${_serif[@]}"; do
    install -m644 "$fnt" "${pkgdir}"/usr/share/fonts/"${pkgname}"
  done

  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m644 45-noto-serif.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-noto-serif.conf
  install -m644 90-tt-noto-serif.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-noto-serif.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-noto-serif.conf .
  ln -s ../conf.avail/90-tt-noto-serif.conf .
}
