# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: bohoomil <@zoho.com>
# Contributor: julroy67 <julroy67@gmail.com>

pkgname=ttf-noto-fonts-emoji-ib
_commit=1da485d
pkgver=1.32
pkgrel=1
epoch=1
pkgdesc="Google Noto emoji fonts."
arch=(any)
url="https://www.google.com/get/noto/"
license=(Apache)
depends=(fontconfig)
conflicts=(noto-fonts-emoji)
provides=(noto-fonts-emoji)
makedepends=(cairo optipng python2-fonttools nototools imagemagick zopfli python2-setuptools)
#source=("noto-emoji::git+https://github.com/googlei18n/noto-emoji.git")
source=("https://github.com/googlei18n/noto-emoji/archive/$_commit.zip")
sha256sums=('ee4c53e52254974a0d10cfe9625467567a4fe46084c1355f767a7cc7696223ac')

prepare() {
  cd noto-emoji-*

  find -name '*.py' | xargs sed -e 's|/usr/bin/env python|/usr/bin/env python2|' -i
  sed -e 's|python|python2|g' -e 's|ttx "$<"|ttx2 "$<"|g' -i Makefile
}

build() {
  cd noto-emoji-*
  make -j4
}

package() {
  cd noto-emoji-*
  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 NotoColorEmoji.ttf fonts/NotoEmoji-Regular.ttf \
    "$pkgdir"/usr/share/fonts/"${pkgname}"
}
