# Maintainer: bohoomil <@zoho.com>

pkgname=otf-andada-ibx
pkgver=1.003
pkgrel=5
depends=('fontconfig')
pkgdesc="Andada is a text font with an organic-slab serif, hybrid style, a solid design of medium stroke contrast. OpenType version."
conflicts=('ttf-andada-ibx')
url="http://www.huertatipografica.com/"
arch=('any')
groups=('infinality-bundle-fonts-extra')
license=('custom:OFL')
install=$pkgname.install
# http://www.fontsquirrel.com (otf)
# http://www.fontsc.com (ttf)
source=(http://bohoomil.com/src/andada_otf.zip{,.sig}
        45-andada.conf
        90-non-tt-andada.conf)
validgpgkeys=('A9244FB5E93F11F0E975337FAE6866C7962DDE58')
sha1sums=('97e80bfd3df758ead7d4906fca4cc02d48cbc219'
          'SKIP'
          'cb3f7a353211156a65e22c2c4d6113fb3179a300'
          '684d4b6c2fc234285935ee8b6a8a7dfa18d5be94')

package(){
  cd "${srcdir}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "SIL Open Font License.txt" \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.otf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-andada.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-andada.conf
  install -m644 90-non-tt-andada.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-andada.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-andada.conf .
  ln -s ../conf.avail/90-non-tt-andada.conf .
}
