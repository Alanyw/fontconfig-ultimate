# Maintainer: Shanto <shanto@hotmail.com>
# Contributor: JIN Xiao-Yong <jinxiaoyong@gmail.com>
# Contributor: Andre Fettouhi <A.Fettouhi@gmail.com>
# Maintainer: bohoomil <@zoho.com>

_pkgbasename=freetype2
pkgname=lib32-$_pkgbasename-infinality-ultimate
pkgver=2.5.0.1
pkgrel=4
pkgdesc="TrueType font rendering library with Infinality patches and custom settings by bohoomil (32-bit, infinality-bundle)."
arch=(x86_64)
license=('GPL' 'CC-BY-SA')
url="http://freetype.sourceforge.net"
groups=('infinality-bundle')
makedepends=(gcc-multilib)
conflicts=('lib32-freetype2' 'lib32-freetype2-infinality' 'lib32-freetype2-git-infinality' 'lib32-freetype2-ubuntu')
provides=('lib32-freetype2' 'lib32-freetype2-infinality')
options=('!libtool')
source=(http://download.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2
        freetype-2.2.1-enable-valid.patch
        freetype-2.4.12-pkgconfig.patch
        infinality-2.5.patch
        autofit.patch)

prepare(){
  cd "${srcdir}/freetype-${pkgver}"

  patch -Np1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch"
  patch -Np1 -i "${srcdir}/freetype-2.4.12-pkgconfig.patch"
  patch -Np1 -i "${srcdir}/infinality-2.5.patch"

  # upstream post-release fixes
  patch -Np1 -i "${srcdir}/autofit.patch"
}


build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/freetype-${pkgver}"

  ./configure \
    --prefix=/usr \
    --without-png \
    --libdir=/usr/lib32
  make
}

package() {
  depends=('lib32-zlib' 'lib32-bzip2' $_pkgbasename-infinality-ultimate)

  cd "${srcdir}/freetype-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}

md5sums=('c72e9010b1d986d556fc0b2b5fcbf31a'
         '214119610444c9b02766ccee5e220680'
         'd821725c1c99bacd76dc97e91d7d4172'
         'bd98f04c25d3223b238cd9c713eb3b6a'
         '21aef34c08c34cf53c58b4bee52efd40')
