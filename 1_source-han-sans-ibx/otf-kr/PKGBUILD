# Maintainer: bohoomil <@zoho.com>

pkgname=otf-source-han-sans-kr-ibx
pkgver=1.003
pkgrel=1
depends=('fontconfig')
pkgdesc="A set of Pan-CJK fonts designed to complement Source Sans Pro. Korean subset, OpenType/CFF (OTF) fonts."
url="http://www.adobe.com/"
arch=('any')
license=('Apache')
conflicts=('adobe-source-han-sans-kr-fonts')
replaces=('adobe-source-han-sans-kr-fonts')
groups=('infinality-bundle-fonts-extra' 'source-han-sans-cjk')
install=$pkgname.install
source=("https://github.com/adobe-fonts/source-han-sans/archive/${pkgver}R.tar.gz"
        40-source-han-sans-kr.conf
        90-non-tt-source-han-sans-kr.conf)
sha1sums=('494a464e71d20191e1660313f00dbc1f82e2896f'
          '67e8781b8bb9917d17594f7b2ff6c53cea0f499a'
          '672e4defaed9da4b4da4cda152e9b3c7ff89de40')

package(){
  cd "source-han-sans-${pkgver}R"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 LICENSE.txt \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 SubsetOTF/KR/*.otf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"


  cd "${srcdir}"
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 40-source-han-sans-kr.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-source-han-sans-kr.conf
  install -m644 90-non-tt-source-han-sans-kr.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-source-han-sans-kr.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-source-han-sans-kr.conf .
  ln -s ../conf.avail/90-non-tt-source-han-sans-kr.conf .
}
