# Maintainer: bohoomil <@zoho.com>

pkgname=otf-source-han-sans-cn-ibx
pkgver=1.001
pkgrel=1
depends=('fontconfig')
pkgdesc="A set of Pan-CJK fonts designed to complement Source Sans Pro. Simplified Chinese subset, OpenType/CFF (OTF) fonts."
url="http://www.adobe.com/"
arch=('any')
license=('Apache')
conflicts=('adobe-source-han-sans-cn-fonts')
replaces=('adobe-source-han-sans-cn-fonts')
groups=('infinality-bundle-fonts-extra' 'source-han-sans-cjk')
install=$pkgname.install
source=("https://github.com/adobe-fonts/source-han-sans/archive/${pkgver}R.tar.gz"
        40-source-han-sans-cn.conf
        90-non-tt-source-han-sans-cn.conf)
sha1sums=('26a72040896f4bdcc4a6ecaa64cd67a764d88aef'
          '92ead7d591ed97268359694e79c8cef126630c8e'
          '26c2e070827f912965b0054f94edaab5808fefc8')

package(){
  cd "source-han-sans-${pkgver}R"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 LICENSE.txt \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 SubsetOTF/CN/*.otf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 40-source-han-sans-cn.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-source-han-sans-cn.conf
  install -m644 90-non-tt-source-han-sans-cn.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-source-han-sans-cn.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-source-han-sans-cn.conf .
  ln -s ../conf.avail/90-non-tt-source-han-sans-cn.conf .
}
