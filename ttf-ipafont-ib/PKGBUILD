# Contributor: ponsfoot <cabezon dot hashimoto at gmail dot com>
# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-ipafont-ib
pkgver=003.03
_pkgver=${pkgver/./}
pkgrel=3
pkgdesc="Japanese outline (P)Mincho and (P)Gothic fonts."
arch=('any')
url="http://ossipedia.ipa.go.jp/ipamjfont/"
license=('custom')
depends=('fontconfig')
groups=('infinality-bundle-fonts')
conflicts=('otf-ipafont' 'otf-ipaexfont')
replaces=('otf-ipafont' 'otf-ipaexfont')
provides=('otf-ipafont')
install=${pkgname}.install
source=("http://download.forest.impress.co.jp/pub/library/i/ipafont/10483/IPAfont${_pkgver}.zip"
        40-ipafont.conf
        90-tt-ipafont.conf)
sha1sums=('9d7558a84b2dac7bcd1aa650f59b4af9cb4d0ea9'
          '1383cf2f5568da98dd508800d25f1cea9e23097e'
          '5020dbfe2444e6147b029618530c8d1241cee817')

package() {
  cd "${srcdir}"/"IPAfont${_pkgver}"

  install -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 IPA_Font_License_Agreement_v1.0.txt \
    Readme_IPAfont"${_pkgver}".txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../40-ipafont.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-ipafont.conf
  install -m644 ../90-tt-ipafont.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-ipafont.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-ipafont.conf .
  ln -s ../conf.avail/90-tt-ipafont.conf .
}
