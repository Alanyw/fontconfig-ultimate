# $Id: PKGBUILD 82253 2013-01-13 15:02:45Z foutrelis $
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Andreas Zwinkau
# Contributor: rabyte <rabyte__gmail>
# Contributor: Stefan Husmann < stefan-husmann@t-online.de>
# Contributor: bohoomil <@zoho.com>

pkgname=ttf-liberation-ib
pkgver=2.00.1
pkgrel=4
pkgdesc='Red Hats Liberation fonts for infinality-bundle-fonts (inc. Liberation Sans Narrow).'
arch=('any')
license=('custom:OFL')
url='https://www.redhat.com/promo/fonts/'
depends=('fontconfig')
groups=('infinality-bundle-fonts')
conflicts=('ttf-liberation')
replaces=('ttf-liberation')
provides=('ttf-font' 'ttf-liberation')
install=$pkgname.install
source=("https://fedorahosted.org/releases/l/i/liberation-fonts/liberation-fonts-ttf-$pkgver.tar.gz"
        "https://fedorahosted.org/releases/l/i/liberation-fonts/liberation-fonts-ttf-1.07.3.tar.gz"
        30-ttf-liberation-mono.conf
        30-ttf-liberation-sans.conf
        30-ttf-liberation-sans-narrow.conf
        30-ttf-liberation-serif.conf)

package() {
  cd "$srcdir/liberation-fonts-ttf-$pkgver"

  install -d "$pkgdir/usr/share/fonts/$pkgname/"
  install -m644 *.ttf "$pkgdir/usr/share/fonts/$pkgname/"

  cd "$srcdir/liberation-fonts-ttf-1.07.3"
  install -m644 *Narrow-*.ttf "$pkgdir/usr/share/fonts/$pkgname/"

  # install fontconfig files
  install -Dm0644 ../30-ttf-liberation-mono.conf \
    "$pkgdir/etc/fonts/conf.avail/30-ttf-liberation-mono.conf"
  install -Dm0644 ../30-ttf-liberation-sans.conf \
    "$pkgdir/etc/fonts/conf.avail/30-ttf-liberation-sans.conf"
  install -Dm0644 ../30-ttf-liberation-sans-narrow.conf \
    "$pkgdir/etc/fonts/conf.avail/30-ttf-liberation-sans-narrow.conf"
  install -Dm0644 ../30-ttf-liberation-serif.conf \
    "$pkgdir/etc/fonts/conf.avail/30-ttf-liberation-serif.conf"

  # install license
  install -Dm644 "${srcdir}/liberation-fonts-ttf-${pkgver}/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

md5sums=('5c781723a0d9ed6188960defba8e91cf'
         'b3174b11c2b6a341f5c99b31088bd67b'
         '6ac8f757e8e77bf3f58a957637d87562'
         'bda9b7b1c25aa27c224f254c3b7d672d'
         'a01d43319c3abac796e99812975bf0a1'
         '6bbc90661e1867881027a07624dc9244')
