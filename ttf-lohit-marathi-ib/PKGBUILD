# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-lohit-marathi-ib
_name=lohit-marathi-ttf
pkgver=2.94.0
pkgrel=1
depends=('fontconfig')
pkgdesc="Lohit Marathi font."
url="https://fedorahosted.org/lohit/"
arch=('any')
conflicts=('lohit-fonts')
groups=('infinality-bundle-fonts')
license=('custom:OFL')
source=("https://fedorahosted.org/releases/l/o/lohit/${_name}-${pkgver}.tar.gz"
        40-lohit-marathi.conf
        66-lohit-marathi.conf
        90-tt-lohit-marathi.conf)
sha1sums=('e6623e5a1eaa0f668a9734396a0c154dd56a4eb4'
          '79ecfc93fbb7187adad426f292132060f237834d'
          '4bafd004e53d74590731a73ccf88912f134e4de1'
          'b08b1851e340f380a8b366c1d836dbf64c5da04d')

package(){
  cd "${_name}-${pkgver}"

  install -D -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -D -m644 40-lohit-marathi.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-lohit-marathi.conf
  install -D -m644 66-lohit-marathi.conf \
    "${pkgdir}"/etc/fonts/conf.avail/66-lohit-marathi.conf
  install -D -m644 90-tt-lohit-marathi.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-lohit-marathi.conf

  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-lohit-marathi.conf .
  ln -s ../conf.avail/66-lohit-marathi.conf .
  ln -s ../conf.avail/90-tt-lohit-marathi.conf .
}
