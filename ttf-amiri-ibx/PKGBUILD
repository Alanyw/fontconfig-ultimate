# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-amiri-ibx
_name=amiri
pkgver=0.108
pkgrel=1
depends=('fontconfig')
pkgdesc="Amiri is a high quality classical Arabic typeface in Naskh style for typesetting books and other running text."
url="http://www.amirifont.org/"
arch=('any')
license=('custom:OFL')
groups=('infinality-bundle-fonts-extra')
source=(https://github.com/khaledhosny/${_name}-font/releases/download/0.108/${_name}-${pkgver}.zip
        40-amiri.conf
        90-tt-amiri.conf)
sha1sums=('127bac78b16b8018b1a91b6e797bbd67d233a483'
          '2e03da2a69ffdf2e81614531a9f867f69c435d06'
          'fd736e05c0fb8f0eb0e8d12f7654373f88f1f029')

package() {
  cd "${_name}-${pkgver}"

  install -D -m644 OFL.txt \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -d -m755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -D -m644 40-amiri.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-amiri.conf
  install -D -m644 90-tt-amiri.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-amiri.conf

  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-amiri.conf .
  ln -s ../conf.avail/90-tt-amiri.conf .
}

