# Contributor: Biru Ionut <biru.ionut at gmail.com>
# Contributor: Paul Bredbury <brebs@sent.com>
# Maintainer: Andrea Fagiani <andfagiani {at} gmail {dot} com>
# Maintainer: bohoomil <@zoho.com>

_pkgbasename=cairo
pkgname=lib32-$_pkgbasename-infinality-ultimate
pkgver=1.12.16
pkgrel=6
groups=('infinality-bundle')
pkgdesc="Cairo vector graphics library with respect-fontconfig and Ubuntu patches (32-bit, infinality-bundle)"
arch=('x86_64')
url="https://launchpad.net/ubuntu/precise/+source/cairo"
groups=('infinality-bundle-multilib')
license=('LGPL' 'MPL')
makedepends=('gcc-multilib' 'lib32-gtk2' 'lib32-mesa-libgl')
depends=('lib32-libpng' 'lib32-libxext' 'lib32-libxrender'
         'lib32-fontconfig-infinality-ultimate'  'lib32-pixman'
         'lib32-glib2' 'lib32-mesa' 'lib32-libgl'
         $_pkgbasename-infinality-ultimate)
provides=("lib32-cairo=$pkgver")
conflicts=('lib32-cairo' 'lib32-cairo-ubuntu')
options=('!libtool')
source=(http://cairographics.org/releases/$_pkgbasename-$pkgver.tar.xz
        cairo-respect-fontconfig_pb.patch
        http://archive.ubuntu.com/ubuntu/pool/main/c/cairo/cairo_1.12.16-0ubuntu2.debian.tar.gz)
sha1sums=('4f6e337d5d3edd7ea79d1426f575331552b003ec'
          'db7bd3e7f3cbb30dafe4ed6604ade944163fbfbe'
          '95d873fda436a1cc29cda1310f155ef0e3258c05')

prepare() {
  cd $srcdir/$_pkgbasename-$pkgver

  for _f in $(cat $srcdir/debian/patches/series) ; do
    patch -Np1 -i $srcdir/debian/patches/$_f
  done

  patch -Np1 -i $srcdir/cairo-respect-fontconfig_pb.patch

  sed '20 aAM_PROG_AR' -i $srcdir/$_pkgbasename-$pkgver/configure.ac
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $srcdir/$_pkgbasename-$pkgver

  autoreconf -if

  # configure options
  # --enable-svg
  # --enable-ps
  # --enable-pdf
  # --enable-xlib-xcb
  # --enable-gobject

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-tee \
    --enable-gl \
    --enable-egl
  make
}

package() {
  cd $srcdir/cairo-$pkgver
  make DESTDIR=$pkgdir install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}

