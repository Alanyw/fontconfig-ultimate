# Contributor: Biru Ionut <biru.ionut at gmail.com>
# Contributor: Paul Bredbury <brebs@sent.com>
# Maintainer: Andrea Fagiani <andfagiani {at} gmail {dot} com>
# Maintainer: bohoomil <@zoho.com>

pkgname=lib32-$_name-infinality-ultimate
_name=cairo
_rev=1.13.0~20140204
_url=launchpad.net/ubuntu/utopic/+source
pkgver=1.13.1
_upver=1.12.6
pkgrel=1
groups=('infinality-bundle')
pkgdesc="Cairo vector graphics library with respect-fontconfig and Ubuntu patches (32-bit, infinality-bundle)"
arch=('x86_64')
url="https://launchpad.net/ubuntu/precise/+source/cairo"
groups=('infinality-bundle-multilib')
license=('LGPL' 'MPL')
makedepends=('gcc-multilib' 'lib32-gtk2' 'lib32-mesa-libgl')
depends=('lib32-libpng' 'lib32-libxext' 'lib32-libxrender'
         'lib32-fontconfig-infinality-ultimate'  'lib32-pixman'
         'lib32-glib2' 'lib32-mesa' 'lib32-libgl'
         $_name-infinality-ultimate)
provides=("lib32-cairo=$pkgver")
conflicts=('lib32-cairo' 'lib32-cairo-ubuntu')
options=('!libtool')
source=("https://${_url}/${_name}/${_rev}-0ubuntu1/+files/${_name}_${_rev}.orig.tar.xz"
        "https://${_url}/${_name}/${_rev}-0ubuntu1/+files/${_name}_${_rev}-0ubuntu1.debian.tar.gz"
        cairo-webkit-html5-fix.patch
        cairo-respect-fontconfig_pb.patch)
sha1sums=('0c88360abce7d6f302086360da77612fe3b11eaa'
          '5b616586068a6ff5ef2f3e2f6cd8495e2eda66b2'
          '5bff494f52a16114f4cf6d04bfb0b9d7c4e9da23'
          'db7bd3e7f3cbb30dafe4ed6604ade944163fbfbe')

prepare() {
  cd "${_name}-${pkgver}"

  patch -Np1 -i $srcdir/cairo-respect-fontconfig_pb.patch
  patch -Np1 -i $srcdir/cairo-webkit-html5-fix.patch

  for _f in $(cat $srcdir/debian/patches/series) ; do
    patch -Np1 -i $srcdir/debian/patches/$_f
  done

  sed '20 aAM_PROG_AR' -i $srcdir/$_name-$pkgver/configure.ac

  autoreconf -vif
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${_name}-${pkgver}"

  # configure options
  # --enable-svg
  # --enable-ps
  # --enable-pdf
  # --enable-xlib-xcb
  # --enable-gobject

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --disable-lto \
    --enable-tee \
    --enable-gl \
    --enable-egl
  make
}

package() {
  cd "${_name}-${pkgver}"
  make DESTDIR=$pkgdir install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
