# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: bohoomil <@zoho.com>

pkgname=t1-adobe-utopia-ib
pkgver=1.0.2
pkgrel=2
pkgdesc="Adobe Utopia font. Type 1 version."
arch=(any)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('fontconfig')
groups=('infinality-bundle-fonts')
install=$pkgname.install
source=(${url}/releases/individual/font/font-adobe-utopia-type1-1.0.2.tar.bz2)
sha1sums=('565cb66523f59f02da02aa9e74604634a2113643')

build() {
  cd "${srcdir}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      ./configure --prefix=/usr \
        --with-fontdir=/usr/share/fonts/${pkgname}
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-*}"
      popd
    fi
  done
  rm -f "${pkgdir}"/usr/share/fonts/${pkgname}/fonts.*
}
