# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-raleway-ibx
pkgver=3.0
pkgrel=1
depends=('fontconfig')
pkgdesc="Raleway is an elegant sans-serif typeface family. Initially designed by Matt McInerney as a single thin weight, it was expanded into a 9 weight family by Pablo Impallari and Rodrigo Fuenzalida."
url="http://openfontlibrary.org/"
arch=('any')
license=('OFL-1.1')
groups=('infinality-bundle-fonts-extra')
conflicts=('otf-raleway' 'ttf-impallari-raleway-family' 'ttf-raleway-font-family')
replaces=('ttf-impallari-raleway-family' 'ttf-raleway-font-family')
source=("http://www.impallari.com/media/uploads/prosources/update-100-source.zip"
        45-raleway.conf
        90-tt-raleway.conf)
install=${pkgname}.install
sha1sums=('986434193552179b6b58346d140d0806a0a891d8'
          'c7a432f074c3c4f0182c10900dc614f7dd538bc9'
          'd3aed36d4f16183f494722ffd44a1e7d9601a5f5')

package() {
  cd "${srcdir}"/"raleway-family-v3.0"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -d -m755 "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../45-raleway.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-raleway.conf
  install -m644 ../90-tt-raleway.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-raleway.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-raleway.conf .
  ln -s ../conf.avail/90-tt-raleway.conf .
}

