# Maintainer: Shanto <shanto@hotmail.com>
# Contributor: JIN Xiao-Yong <jinxiaoyong@gmail.com>
# Contributor: Andre Fettouhi <A.Fettouhi@gmail.com>
# Maintainer: bohoomil <@zoho.com>

_pkgbasename=freetype2
pkgname=lib32-$_pkgbasename-infinality-ultimate
pkgver=2.5.4
pkgrel=2
_patchrel=2014.12.17
pkgdesc="TrueType font rendering library with Infinality patches and custom settings by bohoomil (32-bit, infinality-bundle)."
arch=(x86_64)
license=('GPL' 'MIT')
url="http://freetype.sourceforge.net"
groups=('infinality-bundle-multilib')
depends=('lib32-zlib' 'lib32-bzip2' 'lib32-libpng' 'lib32-harfbuzz' $_pkgbasename-infinality-ultimate)
makedepends=(gcc-multilib)
conflicts=('lib32-freetype2' 'lib32-freetype2-infinality' 'lib32-freetype2-git-infinality'
           'lib32-freetype2-ubuntu')
provides=('lib32-freetype2' 'lib32-freetype2-infinality')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        01-freetype-2.5.4-enable-valid.patch
        02-ftsmooth-2.5.4.patch
        "03-upstream-${_patchrel}.patch"
        "04-infinality-2.5.4-${_patchrel}.patch")
sha1sums=('f30b929e2edc5d7ca64c1ccc65f0c2068e84c5e2'
          'abf7a8f726ad6359533651a8942636880febf9f6'
          '06f28588d3ea03445bc86aa55f5ec97954997a5e'
          '1234a1b47bc39fb1afe7518925cee41a1f26b5ae'
          '0f57a47247f212e9ed957c816c20a564d29a2606')

prepare() {
  cd "freetype-${pkgver}"

  patches=(01-freetype-2.5.4-enable-valid.patch
           02-ftsmooth-2.5.4.patch
           "03-upstream-${_patchrel}.patch"
           "04-infinality-2.5.4-${_patchrel}.patch")

  # infinality & post release fixes
  for patch in "${patches[@]}"; do
    patch -Np1 -i ${srcdir}/"${patch}"
  done
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "freetype-${pkgver}"

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32
  make
}

package() {
  cd "freetype-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
