# Maintainer: Shanto <shanto@hotmail.com>
# Contributor: JIN Xiao-Yong <jinxiaoyong@gmail.com>
# Contributor: Andre Fettouhi <A.Fettouhi@gmail.com>
# Maintainer: bohoomil <@zoho.com>

_pkgbasename=freetype2
pkgname=lib32-$_pkgbasename-infinality-ultimate
pkgver=2.5.3
pkgrel=2
pkgdesc="TrueType font rendering library with Infinality patches and custom settings by bohoomil (32-bit, infinality-bundle)."
arch=(x86_64)
license=('GPL' 'MIT')
url="http://freetype.sourceforge.net"
groups=('infinality-bundle-multilib')
makedepends=(gcc-multilib)
conflicts=('lib32-freetype2' 'lib32-freetype2-infinality' 'lib32-freetype2-git-infinality' 'lib32-freetype2-ubuntu')
provides=('lib32-freetype2' 'lib32-freetype2-infinality')
options=('!libtool')
source=(http://download.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2
        freetype-2.2.1-enable-valid.patch
        freetype-2.5.2-pkgconfig.patch
        infinality-2.5.2.patch)

prepare(){
  cd "${srcdir}/freetype-${pkgver}"

  patch -Np1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch"
  patch -Np1 -i "${srcdir}/freetype-2.5.2-pkgconfig.patch"
  patch -Np1 -i "${srcdir}/infinality-2.5.2.patch"
}


build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/freetype-${pkgver}"

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32
  make
}

package() {
  depends=('lib32-zlib' 'lib32-bzip2' 'lib32-libpng' $_pkgbasename-infinality-ultimate)

  cd "${srcdir}/freetype-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}

md5sums=('d6b60f06bfc046e43ab2a6cbfd171d65'
         '214119610444c9b02766ccee5e220680'
         '4fd922532b451d862a5374c2cc801581'
         'ac23b2f92d084d2fff1c3766b9c955dc')
