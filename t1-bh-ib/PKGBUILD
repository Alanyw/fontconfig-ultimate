# $Id: PKGBUILD 150376 2012-02-17 09:55:00Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: bohoomil <@zoho.com>

pkgname=t1-bh-ib
pkgver=1.0.1
pkgrel=2
pkgdesc="Luxi Sans, Serif and Mono families by Bigelow & Holmes. Type 1 version."
arch=(any)
depends=('fontconfig')
groups=('infinality-bundle-fonts')
license=('custom')
url="http://xorg.freedesktop.org/"
install=$pkgname.install
source=(${url}/releases/individual/font/font-bh-type1-1.0.1.tar.bz2)
md5sums=('374d954c61363206c93d1ce7e9445c31')

build() {
  cd "${srcdir}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      ./configure --prefix=/usr \
        --with-fontdir=/usr/share/fonts/${pkgname}
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-*}"
      popd
    fi
  done
  rm -f "${pkgdir}"/usr/share/fonts/${pkgname}/fonts.*
}
